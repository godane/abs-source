# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: bekks <eduard.warkentin@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=upx
pkgver=3.05
pkgrel=1
pkgdesc="Ultimate executable compressor"
arch=('i686' 'x86_64')
url="http://upx.sourceforge.net/"
license=('GPL')
depends=('gcc-libs' 'zlib')
makedepends=('perl' 'ucl')
source=(http://upx.sourceforge.net/download/${pkgname}-${pkgver}-src.tar.bz2
	http://downloads.sourceforge.net/sevenzip/lzma465.tar.bz2)
md5sums=('1f0ca94c8c26a816402274dd7e628334'
	 '29d5ffd03a5a3e51aef6a74e9eafb759')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}-src

	make UPX_LZMA_VERSION=0x465 UPX_LZMADIR=${srcdir} all || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}-src

	install -Dm755 src/upx.out ${pkgdir}/usr/bin/upx
	install -Dm644 doc/upx.1 ${pkgdir}/usr/share/man/man1/upx.1
}
