# $Id: PKGBUILD 16561 2010-05-03 08:53:24Z rvanharen $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=sakura
pkgver=2.3.8
pkgrel=1
pkgdesc="A terminal emulator based on GTK and VTE"
arch=('i686' 'x86_64')
url="http://pleyades.net/david/sakura.php"
license=('GPL')
depends=('vte' 'libxft' 'desktop-file-utils')
makedepends=('cmake')
source=(http://pleyades.net/david/projects/$pkgname/$pkgname-$pkgver.tar.bz2 \
	blinkingcursor.patch)
install=sakura.install
md5sums=('5ce4c9498ea5eb909dbd03617087202e'
         '0cfb0200a194a664afa67e240fc80cba')

build() {
  cd $srcdir/${pkgname}-${pkgver}

  # Set default font size a bit smaller
  sed -i 's|#define DEFAULT_FONT "Bitstream Vera Sans Mono 14"|#define DEFAULT_FONT "Bitstream Vera Sans Mono 10"|g' src/sakura.c
 
  # build & install	
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE . || return 1
  make || return 1
}

package() {
  cd $srcdir/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install || return 1

  # extract the keybindings from the installed documentation, rest is only relevant during build time
  awk '/^Keybindings/{f="'${pkgdir}'/usr/share/doc/'${pkgname}'/KEYBINDINGS"} f{print > f} /^END/' \
        ${pkgdir}/usr/share/doc/${pkgname}/INSTALL
  rm ${pkgdir}/usr/share/doc/${pkgname}/INSTALL
}

