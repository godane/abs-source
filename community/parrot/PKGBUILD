# $Id: PKGBUILD 17116 2010-05-19 17:11:08Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributer: mpie <michael.kyne-phillips1@ntlworld.com>

pkgname=parrot
pkgver=2.4.0
_rel=devel
#_rel=stable
pkgrel=1
pkgdesc="standalone virtual machine that can be used to execute bytecode compiled dynamic languages"
arch=(i686 x86_64)
url="http://www.parrotcode.org/"
license=('GPL')
depends=('gmp' 'gdbm' 'readline' 'icu' 'openssl')
optdepends=('freeglut')
options=(!emptydirs !makeflags)
source=(ftp://ftp.parrot.org/pub/parrot/releases/${_rel}/$pkgver/parrot-$pkgver.tar.gz)
md5sums=('596c62109f80e1c4b6a5919553dc801b')

build() {
  cd $srcdir/$pkgname-$pkgver

  perl Configure.pl --prefix=/usr --parrot_is_shared

  find -type f -name Makefile | while read F; do
    grep "$srcdir" $F  && sed -i "s#-Wl,-rpath=$srcdir/parrot-$pkgver/blib/lib##" $F
    grep "$srcdir" $F  && sed -i "s#-rpath=$srcdir/parrot-$pkgver/blib/lib##" $F
  done

  OLD_LIB_PATH=$LD_LIBRARY_PATH
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:$srcdir/parrot-$pkgver/blib/lib

  make all parrot_utils docs html || return 1

  export LD_LIBRARY_PATH=`pwd`/blib/lib
  make DESTDIR=$pkgdir install-dev || return 1

  sed -i "s#$srcdir#/usr/src#" $pkgdir/usr/lib/parrot/$pkgver/tools/lib/Parrot/Config/Generated.pm || return 1

  export LD_LIBRARY_PATH=$OLD_LIB_PATH
}
