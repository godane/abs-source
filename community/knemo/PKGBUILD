# $Id: PKGBUILD 15267 2010-04-09 12:31:08Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Zamprogno <stefano dot zamprogno at gmail dot com>

pkgname=knemo
pkgver=0.6.2
pkgrel=2
pkgdesc="The KDE Network Monitor"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=12956"
license=('GPL')
depends=('kdebase-workspace' 'wireless_tools' 'iw')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
options=('libtool')
source=(http://www.kde-apps.org/CONTENT/content-files/12956-$pkgname-$pkgver.tar.bz2)
md5sums=('9a4662c5a4f82d913684465c25db0f50')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr    
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1

  # remove knemo autostart
  rm -rf ${pkgdir}/usr/share/autostart
}
