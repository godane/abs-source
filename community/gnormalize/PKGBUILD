# $Id: PKGBUILD 17913 2010-05-26 11:53:59Z cbrannon $
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor:JZsolt Varadi (karmaxxl) 

pkgname=gnormalize
pkgver=0.63
pkgrel=2
pkgdesc="An audio normalizer, converter, encoder/decoder, ripper, tagger and player"
arch=(any)
url="http://gnormalize.sourceforge.net/"
license=('GPL')
depends=(perl cddb_get desktop-file-utils gtk2-perl hicolor-icon-theme)
makedepends=(lzma)
optdepends=('faac: full MP4 format'
            'flac: FLAC encoding, FLAC playing in conjunction with mplayer'
            'lame: MP3 encoding'
            'mac: full Monkeys Audio Codec support (available in the AUR)'
            'mpg321: MP3 playing'
            'mplayer: FLAC/WAV playing'
            'musepack-tools: full Musepack support'
            'perl-mp3-info: MP3 ID3 tags'
            'vorbis-tools: full OGG Vorbis support'
            'cdcd: CD playing'
            'cdparanoia: CD ripping'
            'cdrkit: CD ripping - alternative to cdparanoia'
            'wavegain: sound normalization')
install=gnormalize.install
source=(http://downloads.sourceforge.net/sourceforge/gnormalize/${pkgname}-${pkgver}.tar.gz
        gnormalize.desktop)
md5sums=('25ca7520731e61a8b7f4b7fa61928d54' '81c6dffab2139816919842e042705884')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 gnormalize "${pkgdir}/usr/bin/gnormalize" || return 1
  lzma -qd gnormalize.1.lzma || return 1
  install -Dm644 gnormalize.1 "${pkgdir}/usr/share/man/man1/gnormalize.1" || return 1
  install -d "${pkgdir}/usr/share/gnormalize/animations" || return 1
  install -m644 animations/*.gif "${pkgdir}/usr/share/gnormalize/animations/" \
    || return 1
  install -Dm644 README "${pkgdir}/usr/share/doc/gnormalize/README" || return 1
  install -Dm644 icons/gnormalize.png  \
    "${pkgdir}/usr/share/pixmaps/gnormalize.png" || return 1
  for i in 16 32 48 ; do
    install -Dm644 icons/gnormalize-${i}.png \
      "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/gnormalize.png" \
      || return 1
  done
  install -Dm644 "${srcdir}/gnormalize.desktop" \
    "${pkgdir}/usr/share/applications/gnormalize.desktop" || return 1
}

# vim:set ts=2 sw=2 et:
