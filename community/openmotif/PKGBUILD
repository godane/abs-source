# $Id: PKGBUILD 16932 2010-05-13 22:21:25Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

_automakever=`pacman -Q automake | cut -f 2 -d \  | cut -f 1 -d -`

pkgname=openmotif
pkgver=2.3.3
pkgrel=1
pkgdesc="Open Motif"
arch=('i686' 'x86_64')
url="http://www.motifzone.org/"
license=('GPL')
depends=(libxext libxp libxft libxt)
makedepends=(xbitmaps xproto printproto xextproto automake)
conflicts=(lesstif)
options=('!libtool')
source=(http://www.motifzone.org/files/public_downloads/openmotif/2.3/$pkgver/openmotif-$pkgver.tar.gz)
md5sums=('fd27cd3369d6c7d5ef79eccba524f7be')

build() {
  cd $startdir/src/openmotif-$pkgver

  [ $NOEXTRACT -eq 1 ] || patch -p0 Makefile.in <<EOF
--- Makefile.in.org	2007-06-11 09:32:01.000000000 +0400
+++ Makefile.in	2007-06-19 19:13:04.000000000 +0400
@@ -249,8 +249,7 @@
              include \\
              tools \\
              clients \\
-             doc \\
-             demos
+             doc
 
 AUTOMAKE_OPTIONS = 1.4
 all: all-recursive
EOF

  sed -i 's#if (!png_check_sig(sig, 8))#if (png_sig_cmp(sig, 0, 8))#' lib/Xm/Png.c

  ln -s -f /usr/share/automake-${_automakever}/compile compile
  ln -s -f /usr/share/libtool/config/config.guess config.guess
  ln -s -f /usr/share/libtool/config/config.sub config.sub
  ln -s -f /usr/share/automake-${_automakever}/depcomp depcomp
  ln -s -f /usr/share/automake-${_automakever}/install-sh install-sh
  ln -s -f /usr/share/automake-${_automakever}/missing missing
  ln -s -f /usr/share/libtool/config/ltmain.sh ltmain.sh

  [ $NOEXTRACT -eq 1 ] || autoreconf -i

  [ $CARCH == "x86_64" ] && export CFLAGS="$CFLAGS -fPIC"

  [ $NOEXTRACT -eq 1 ] || ./configure --with-x --prefix=/usr
  make -j1 || return 1

  make DESTDIR=$startdir/pkg install || return 1
  # cleanup
  (cd $startdir/pkg/usr/share/man/man3/ && mv Shell.3 OpenMotif-Shell.3)
}
