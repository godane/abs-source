# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>

pkgname=xiphos
pkgver=3.1.3
pkgrel=2
pkgdesc="A Bible study tool for gtk"
arch=('i686' 'x86_64')
url="http://xiphos.org"
license=('GPL')
depends=('libgsf' 'libglade' 'gtkhtml' 'sword' 'nspr')
makedepends=('xulrunner' 'gnome-doc-utils' 'intltool')
conflicts=('gnomesword')
provides=('gnomesword')
replaces=('gnomesword')
source=(http://downloads.sourceforge.net/sourceforge/gnomesword/$pkgname-$pkgver.tar.gz)
md5sums=('aaded6bc81b29935edd989d204928ba3')
install=$pkgname.install

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --with-gecko=libxul-embedding || return 1
  make || return 1
}
package() {
  cd $srcdir/$pkgname-$pkgver
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install \
    DESTDIR=$pkgdir || return 1
}
