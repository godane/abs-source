# $Id: PKGBUILD 12655 2010-03-10 16:27:45Z ibiru $
# Maintainer : Ionut Biru <ionut@archlinux.ro>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=blueman
pkgver=1.21
pkgrel=4
pkgdesc="GTK+ bluetooth manager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://blueman-project.org"
depends=('python-notify' 'bluez>=4.58' 'dbus-python' 'gtk2>=2.18.5' 'glib2>=2.22.3' \
         'pygobject>=2.20.0' 'startup-notification>=0.10' 'obex-data-server>=0.4.4' \
         'polkit-gnome' 'xdg-utils' 'hicolor-icon-theme')
makedepends=('pyrex>=0.9.8.0' 'pkgconfig' 'intltool')
optdepends=('dnsmasq: For creating PAN network'
	    'gconf: For gconf configuration support'
	    'pulseaudio: To use the pulseaudio plugin'
	    'notification-daemon, libnotify: for desktop notifications'
	    'modemmanager: for modem management service')
options=('!libtool')
install=$pkgname.install
source=(http://download.tuxfamily.org/blueman/${pkgname}-${pkgver}.tar.gz)
build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
	   	--libexecdir=/usr/lib/blueman \
		--with-no-runtime-deps-check
	make || return 1
	make DESTDIR="${pkgdir}" install || return 1
	echo "OnlyShowIn=GNOME;XFCE;" >> ${pkgdir}/etc/xdg/autostart/blueman.desktop
}
md5sums=('26b70341b3d3da28da62c917c8b20377')
