# $Id: PKGBUILD 16917 2010-05-13 18:38:27Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gnome-keyring-sharp
pkgver=1.0.0
pkgrel=2
pkgdesc="A fully managed implementation of libgnome-keyring"
arch=('i686' 'x86_64')
url="http://www.mono-project.com/Libraries"
license=('custom')
depends=('ndesk-dbus>=0.4' 'libgnome-keyring')
makedepends=('gtk-sharp-2')
replaces=('gnome-keyring-sharp-svn')
source=(http://ftp.de.debian.org/debian/pool/main/g/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz
	02_gnome_2.30_compatibility.patch)
md5sums=('4879a228d732a5f65062da068cced21c'
         '932d1a1b60fbedcb15cfa76ffac35184')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wapi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/02_gnome_2.30_compatibility.patch" || return 1
  autoreconf -v -i -s || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${MONO_SHARED_DIR}"
  ln -sf gnome-keyring-sharp-1.0.pc "${pkgdir}/usr/lib/pkgconfig/gnome-keyring-sharp.pc"

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license
}
