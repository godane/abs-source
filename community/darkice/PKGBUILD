# $Id: #
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ganjolinux aka Basalari David <ganjolinux@gmail.com>

pkgname=darkice
pkgver=0.20.1
pkgrel=1
pkgdesc="Live audio streamer. Records audio from an audio interface, encodes it and sends it to a streaming server"
arch=('i686' 'x86_64')
url="http://code.google.com/p/darkice/"
license=('GPL')
backup=('etc/darkice.cfg')
depends=('gcc-libs' 'alsa-lib' 'lame' 'libvorbis' 'faac' 'jack')
source=("http://darkice.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
	'darkiced')
md5sums=('cf4b8f81b9e4a92a8cd57d15fcbb4969'
         'a2608524120e6bb1c704ed4c9165a386')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --without-aacplus \
    --without-twolame
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  install -Dm755 ${srcdir}/darkiced \
    ${pkgdir}/etc/rc.d/darkice || return 1
}
