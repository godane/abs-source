# $Id: PKGBUILD 16395 2010-04-29 14:10:21Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools-modules
pkgver=2010.04.25
_pkgsubver=253928
_kernver=`pacman -Q kernel26 | cut -d . -f 3 | cut -f 1 -d -`
pkgrel=2
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools."
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('GPL')
makedepends=('libdnet' 'icu' 'uriparser')
depends=("kernel26>=2.6.${_kernver}" "kernel26<2.6.`expr ${_kernver} + 1`")
install=$pkgname.install
source=(http://easynews.dl.sourceforge.net/sourceforge/open-vm-tools/open-vm-tools-$pkgver-${_pkgsubver}.tar.gz)
md5sums=('da0ed15d29f4a1f6eca21e8aeeabb476')

build() {
  KERNEL_VERSION="2.6.${_kernver}-ARCH"
  msg "Kernel = $KERNEL_VERSION"

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"

  sed -i 's#-lproc-3.2.7#-lproc-3.2.8#' configure

  ./configure --prefix=/usr

  (cd modules && make modules) || return 1

  for MOD in vmblock  vmhgfs  vmmemctl  vmsync  vmxnet vmci vsock; do
    install -D -m644 modules/linux/$MOD/$MOD.ko $pkgdir/lib/modules/$KERNEL_VERSION/misc/$MOD.ko || return 1
  done

  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='$KERNEL_VERSION'/" $startdir/$pkgname.install
}

