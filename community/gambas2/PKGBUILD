# $Id: PKGBUILD 13073 2010-03-17 23:50:43Z ibiru $
# Maintainer : Biru Ionut <ionut@archlinux.ro>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Toni Foerster <stonerl@skeps.de>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=gambas2
pkgver=2.20.2
pkgrel=1
pkgdesc="A free development environment based on a Basic interpreter."
arch=('i686' 'x86_64')
url="http://gambas.sourceforge.net"
depends=('librsvg' 'postgresql-libs>=8.4.1' 'libmysqlclient' \
         'curl' 'poppler' 'sqlite3' 'sdl_mixer' 'sdl_image' \
         'unixodbc' 'kdelibs3' 'libffi' 'omniorb' 'xdg-utils' 'libxtst' 'libfbclient' 'sqlite2')
makedepends=('intltool' 'mysql' 'postgresql')
license=('GPL2')
options=('!makeflags' '!libtool')
source=(http://downloads.sourceforge.net/gambas/$pkgname-$pkgver.tar.bz2
	'gambas2.desktop' 'gambas2.png')
install=gambas2.install

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ## necessary for corba fix
#  cd gb.corba && autoconf && cd ..
  ##

  . /etc/profile.d/kde3.sh
  . /etc/profile.d/qt3.sh


  ./configure -C --disable-qte --prefix=/usr
  make bindir=${pkgdir}/usr/bin || return 1
  make XDG_UTILS='' DESTDIR=${pkgdir} install || return 1

  ## needed for postinst with xdg-utils
  install -d -m755 ${pkgdir}/usr/share/gambas2/mime || return 1
  install -D -m644 main/mime/* \
    ${pkgdir}/usr/share/gambas2/mime/ || return 1
  ##

  install -d -m755 ${pkgdir}/usr/share/gambas2/icons || return 1
  install -D -m644 main/mime/application-x-gambas.png \
    ${pkgdir}/usr/share/gambas2/icons/application-x-gambas.png ||  return 1
  install -D -m644 ${srcdir}/gambas2.desktop \
    ${pkgdir}/usr/share/applications/gambas2.desktop || return 1
  install -D -m644 ${srcdir}/gambas2.png \
    ${pkgdir}/usr/share/pixmaps/gambas2.png || return 1

  #fix help
  chown root:root -R ${pkgdir}/usr/share/gambas2/help/help || return 1
}
md5sums=('60485fcc37cbe204a5f52fed956e9ef3'
         'e6c2df38b29fcb6ba9ac7ba783688211'
         '32672c984d8487beb13aa5b6bc6868e2')
sha256sums=('3df4ba8c3020cae0d4aef90bbefcf7d81a86e7f7403855211b1f534c47046814'
            'd84755cdc31139dae89efb8d59b7d64aa8d0e045323dffb79cd403ab831c55ef'
            'cab34bf41b9d1d5302a551bbf5936411db7dd14b88736e900344a4a224a5d255')
