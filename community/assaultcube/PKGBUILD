# $Id: PKGBUILD 6537 2009-12-15 12:52:24Z andrea $
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Rabyte <rabyte [at] gmail (dot) com>

pkgname=assaultcube
pkgver=1.0.4
_pkgver=1.0.2
pkgrel=1
pkgdesc="A realistic team oriented multiplayer FPS based on the Cube engine"
arch=('i686' 'x86_64')
url="http://assault.cubers.net/"
license=('ZLIB' 'custom')
replaces=('actioncube')
depends=('sdl_mixer' 'sdl_image' 'mesa' 'openal')
source=(http://downloads.sourceforge.net/actiongame/AssaultCube_v${_pkgver}.tar.bz2
	http://downloads.sourceforge.net/actiongame/AssaultCube_v${pkgver}-Update.tar.bz2
	'assaultcube' 'assaultcube-server' 'assaultcube.desktop' 'assaultcube.png')
noextract=('AssaultCube_v${pkgver}-Update.tar.bz2')
md5sums=('e083182699e65c240d8c7fa051644e6f'
         'd907a279879fed8c21cd3ba83ce984bf'
         'f8c4da495c7d9678a96efee77726813c'
         'ea79f02b85d30ac4c36893cf749caa98'
         'a37dfbe8263f4ef8fe41120196194eae'
         'f688c59ecee2ebc5c589720aa1480765')

build() {
  cd "$srcdir/AssaultCube_v$_pkgver"

  # Apply 1.0.4 update
  tar xf ${srcdir}/AssaultCube_v${pkgver}-Update.tar.bz2

  cd source/src
  make || return 1
  strip ac_{client,server}
  install -D ac_client "$pkgdir/usr/bin/ac_client" || return 1
  install -D ac_server "$pkgdir/usr/bin/ac_server" || return 1
  install -D "$srcdir/assaultcube" "$pkgdir/usr/bin/assaultcube" || return 1
  install -D "$srcdir/assaultcube-server" "$pkgdir/usr/bin/assaultcube-server" || return 1

  mkdir -p "$pkgdir/usr/share/assaultcube/packages"
  cd "$srcdir/AssaultCube_v$_pkgver"
  cp -rf config packages bot "$pkgdir/usr/share/assaultcube" || return 1

  install -Dm644 "$srcdir/assaultcube.png" "$pkgdir/usr/share/pixmaps/assaultcube.png"
  install -Dm644 "$srcdir/assaultcube.desktop" "$pkgdir/usr/share/applications/assaultcube.desktop"
  install -Dm644 docs/package_copyrights.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
