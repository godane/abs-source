# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: pressh <pressh at gmail dot com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=kazehakase
pkgver=0.5.8
pkgrel=2
pkgdesc="GTK+ web browser that uses the Gecko rendering engine"
arch=('i686' 'x86_64')
url="http://kazehakase.sourceforge.jp/"
license=('GPL')
depends=('xulrunner>=1.9.2')
makedepends=('intltool')
options=('!libtool')
source=(http://downloads.sourceforge.jp/${pkgname}/43802/${pkgname}-${pkgver}.tar.gz
	kazehakase-rev3870-xul192-workaround.patch
	kazehakase-0.5.7-external-rev938-libegg-parallel_make.patch)
md5sums=('22d831982ebe010338907381caee8fdf'
	 'd85665b5b8b56e0b9f405422494e3263'
	 'b03ad7840b801ecaf66a83e5e2818015')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -Np0 -i ${srcdir}/kazehakase-rev3870-xul192-workaround.patch || return 1
	patch -Np0 -i ${srcdir}/kazehakase-0.5.7-external-rev938-libegg-parallel_make.patch || return 1

	./autogen.sh || return 1

	./configure --prefix=/usr \
	    --sysconfdir=/etc \
	    --with-gecko-engine=libxul \
	    --disable-gtkmozembed \
	    --with-ruby=no \
	    --with-rgettext=no
	  make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install || return 1
}
