# $Id: PKGBUILD 15710 2010-04-14 11:11:22Z angvp $
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>

pkgname=eclipse-subclipse
pkgver=1.6.10
pkgrel=1
_urlver=47423
pkgdesc="Subversion integration for the Eclipse platform"
arch=('any')
url="http://subclipse.tigris.org/"
license=('EPL')
depends=('eclipse' 'subversion')
source=(http://subclipse.tigris.org/files/documents/906/${_urlver}/site-${pkgver}.zip)
md5sums=('9667577cd7e4c2b1e84f5ce372e1e1cc')


build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f -name *.jar | while read feature ; do
    install -dm755 ${_dest}/${feature/.jar}
    cd ${_dest}/${feature/.jar}
    jar xf ${srcdir}/${feature} || return 1
  done

  # Plugins
  find plugins -type f -exec install -Dm644 {} ${_dest}/{} \;
}

# vim:set ts=2 sw=2 et:
