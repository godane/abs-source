# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=dvdstyler
pkgver=1.8.0.2
pkgrel=1
pkgdesc="Cross-platform DVD authoring application"
arch=('i686' 'x86_64')
url="http://www.dvdstyler.de/"
license=('GPL')
depends=('cdrkit' 'dvdauthor' 'dvd+rw-tools' 'ffmpeg' 'gettext' 'libexif' 'wxgtk' 'wxsvg' 'xine-ui' 'mjpegtools' 'xmlto' 'docbook-xsl' 'libgnomeui')
makedepends=('zip')
optdepends=('dvdisaster: ECC support')
options=('!makeflags')
source=(http://downloads.sourceforge.net/dvdstyler/DVDStyler-${pkgver}.tar.bz2)
md5sums=('a6ca8fa2b5bc3e84a43cf6c520715450')

build() {
	cd ${srcdir}/DVDStyler-${pkgver}

	./configure --prefix=/usr || return 1
	sed -i '/^LINGUAS=/d' locale/Makefile
	sed -i '/^LINGUAS_ALL=/d' locale/Makefile
	sed -i '5i LINGUAS=cs da de el es fr it pt sk' locale/Makefile
	sed -i '6i LINGUAS_ALL=cs da de el es fr it pt sk' locale/Makefile
	make || return 1
}

package() {
	cd ${srcdir}/DVDStyler-${pkgver}

	make DESTDIR=${pkgdir} install || return 1
}
