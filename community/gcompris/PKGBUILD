# $Id: PKGBUILD 16241 2010-04-27 02:48:13Z foutrelis $
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=('gcompris' 'gcompris-data')
pkgver=9.3
pkgrel=1
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('i686' 'x86_64')
url="http://gcompris.net/"
license=('GPL3')
makedepends=('texinfo' 'texi2html' 'intltool' 'gettext' 'gnuchess'
             'pygtk' 'pyxml' 'librsvg' 'gnet' 'python-pysqlite'
             'gnome-python-desktop' 'gstreamer0.10-base-plugins')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/gcompris/$pkgname-$pkgver.tar.gz)
md5sums=('b563e412751e909e9e89d0c396f01f04')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make || return 1
}

package_gcompris() {
  depends=('gcompris-data' 'pygtk' 'pyxml' 'librsvg' 'gnet' 'python-pysqlite'
           'gnome-python-desktop' 'gstreamer0.10-base-plugins')
  optdepends=('gnucap: for computer simulation within the electricity activity'
              'tuxpaint: for the painting activity'
              'gnuchess: for the chess activity')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-exec || return 1
}

package_gcompris-data() {
  pkgdesc="Data files for GCompris"
  #arch=('any')
  install=gcompris-data.install

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-data || return 1

  # Place GCompris menu entry under Education
  sed -i 's|^Categories=.*|Categories=Education;|' \
         "$pkgdir/usr/share/applications/gcompris.desktop" || return 1
}

# vim:set ts=2 sw=2 et:
