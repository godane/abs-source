# $Id: PKGBUILD 12532 2010-03-07 19:58:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Emanuele Rossi <newdna1510@yahoo.it>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=choqok
pkgver=0.9.55
pkgrel=1
pkgdesc="A Twitter/identi.ca/laconica client for KDE"
url="http://choqok.gnufolks.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=(http://ftp.nedit.org/ftp/FreeBSD/ports/local-distfiles/fluffy/$pkgname-$pkgver.tar.bz2)
md5sums=('c3515341edbf8f5ae0a95b0fca56d1e5')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
