# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=2.5
pkgrel=1
pkgdesc="Lightweight, fast web browser"
arch=('i686' 'x86_64')
url="http://www.netsurf-browser.org/"
license=('GPL')
depends=('libmng' 'librsvg' 'curl' 'libglade' 'lcms' 'libjpeg>=7'
	 'libwapcaplet' 'libcss')
makedepends=('re2c' 'libnsbmp' 'libnsgif' 'hubbub')
source=("http://www.netsurf-browser.org/downloads/releases/${pkgname}-${pkgver}-src.tar.gz"
        "lemon.c::http://www.sqlite.org/cvstrac/getfile?f=sqlite/tool/lemon.c&v=1.69"
        "lempar.c::http://www.sqlite.org/cvstrac/getfile?f=sqlite/tool/lempar.c&v=1.69"
        libpng14.patch)
md5sums=('7938f4c59ecf6241b36442b70acd9203'
         '3a8db045f48383021b5311b21b02ba0e'
         '097ade562c5ff523df15e20ea440e879'
         '24d3821ce1df24aa3274b11e448be7b2')

build() {
	# Fix the css parser code
	cd ${srcdir}/${pkgname}/css
	cp ${srcdir}/lem{on,par}.c .
	gcc ${CFLAGS} -o lemon lemon.c || return 1
	./lemon parser.y
	# Build netsurf
	cd ${srcdir}/${pkgname}
	patch -Np1 -i ${srcdir}/libpng14.patch

	sed -i 's#GTK_WIDGET_VISIBLE#gtk_widget_get_visible#' gtk/gtk_download.c
	sed -i 's#GTK_WIDGET_HAS_FOCUS#gtk_widget_has_focus#' gtk/gtk_scaffolding.c
	sed -i 's#GTK_WIDGET_SENSITIVE#gtk_widget_get_sensitive#' gtk/gtk_scaffolding.c
}

package() {
	cd ${srcdir}/${pkgname}
	make install DESTDIR=${pkgdir} PREFIX=/usr NETSURF_USE_HARU_PDF=NO
}
