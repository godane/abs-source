# $Id: PKGBUILD 17343 2010-05-23 12:53:42Z ttopper $
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=roxterm
pkgver=1.18.3
pkgrel=1
pkgdesc="Tabbed, VTE-based terminal emulator"
arch=('i686' 'x86_64')
url="http://roxterm.sourceforge.net/"
license=('GPL3')
depends=('libglade' 'dbus-glib' 'vte' 'hicolor-icon-theme' 'dbus')
makedepends=('docbook-xsl' 'xmlto')
install=roxterm.install
source=(http://downloads.sourceforge.net/roxterm/roxterm-$pkgver.tar.gz)
md5sums=('516bdd93624725156cfa7285ef0afe82')

build() {
  cd "${srcdir}/roxterm-${pkgver}"

  # Fix configure script
  sed -i 's|-a test -f "$withval"|-a -f "$withval"|' configure || return 1

  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "${srcdir}/roxterm-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
