# $Id: PKGBUILD 11282 2010-02-09 02:12:09Z dgriffiths $
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Shahar Weiss <sweiss4@gmx.net>
# Maintainer: Daenyth

pkgname=visualboyadvance
pkgver=1.7.2
pkgrel=7
pkgdesc="An emulator for Gameboy and Gameboy Advance systems."
arch=('i686' 'x86_64')
url="http://vba.ngemu.com/"
license=("GPL")
depends=('libglademm' 'sdl')
makedepends=('nasm')
backup=('/etc/VisualBoyAdvance.cfg')
source=(http://downloads.sourceforge.net/sourceforge/vba/VisualBoyAdvance-src-${pkgver}.tar.gz
        1.7.2-deprecatedsigc++.patch 1.7.2-gcc34.patch 1.7.2-gcc41.patch
	1.7.2-glibc.patch 1.7.2-homedir.patch 1.7.2-uninit.patch)
md5sums=('cc02339e3fd8efd9f23121b0a2f81fd8'
         '95596fbe499c23b8154142b5789cd630'
         'e7c816ac1adf832c8bd3f6b899d6b935'
         '22c90f2d75856c8407f70b19038105a4'
	 'e7a5188b483e2c4d2fee2a5efb088908'
	 'ef02d9be59ab74afa9a8f12e17e359d4'
	 '004c269a9cdbfa0f4b7fd51978f78705')

build() {
  cd ${srcdir}/VisualBoyAdvance-${pkgver}

  patch -Np0 < ${srcdir}/${pkgver}-homedir.patch || return 1
  patch -Np0 < ${srcdir}/${pkgver}-gcc34.patch || return 1
  patch -Np0 < ${srcdir}/${pkgver}-gcc41.patch
  patch -Np0 < ${srcdir}/${pkgver}-deprecatedsigc++.patch || return 1
  patch -Np0 < ${srcdir}/${pkgver}-uninit.patch || return 1
  patch -Np0 < ${srcdir}/${pkgver}-glibc.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --enable-gtk=2.4 || return 1
  make || return 1
  make DESTDIR=${pkgdir} install
}
