# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Benjamin Wild <benwild@gmx.de>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=exaile
pkgver=0.3.1.1
pkgrel=1
pkgdesc="A full-featured media player for GTK+"
arch=('i686' 'x86_64')
url="http://www.exaile.org"
license=('GPL')
depends=('python' \
	 'gstreamer0.10-python' \
	 'gstreamer0.10-good-plugins' \
	 'mutagen' \
	 'dbus-python' \
	 'pygtk>=2.10' \
	 'librsvg')
makedepends=('make')
optdepends=('pycddb: CD metadata retrieval' \
	    'hal: device autodetection' \
	    'gstreamer0.10-bad-plugins: support for more formats' \
	    'gstreamer0.10-ugly-plugins: support for more formats' \
	    'gstreamer0.10-ffmpeg: support for more formats' \
	    'python-sexy: UI enhancements')
source=(https://www.launchpad.net/exaile/0.3.1/0.3.1.1/+download/exaile-${pkgver}.tar.gz)
md5sums=('4f0e6ce72249027d69653456a0707c57')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make PREFIX=/usr DESTDIR=${pkgdir} install

	# fix for clicking files with spaces in names from nautilus
	sed -i "s#%u#%f#" ${pkgdir}/usr/share/applications/exaile.desktop
}
