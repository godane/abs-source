# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Sven Kauber <celeon@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=partimage
pkgver=0.6.8
pkgrel=2
pkgdesc="Partition Image saves partitions in many formats to an image file."
arch=('i686' 'x86_64')
url="http://www.partimage.org/"
license=('GPL')
depends=('libnewt' 'lzo' 'bzip2' 'openssl')
backup=('etc/partimaged/partimagedusers')
install=partimage.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	'partimaged' 'partimaged-gencrt' 'partimage-0.6.8-gcc44.patch')
md5sums=('c13c8ede8cdf7745b97ec5827920ece7'
         'f3b517a62de5540f3b1a8b7edf65b7ec'
         '2316b116227b07efb48266e660ca1ba1'
         'a6430cf98c8f236c41dd3addcde1fcb8')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -p1 -i ${srcdir}/partimage-0.6.8-gcc44.patch || return 1
	./configure --prefix=/usr --sysconfdir=/etc --enable-pam
	make || return 1
	make pamfile || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install || return 1

	install -Dm644 partimaged.pam ${pkgdir}/etc/pam.d/partimaged || return 1
	install -Dm755 ${srcdir}/partimaged ${pkgdir}/etc/rc.d/partimaged || return 1
	install -Dm755 ${srcdir}/partimaged-gencrt ${pkgdir}/usr/bin/partimaged-gencrt || return 1
	chmod 644 ${pkgdir}/etc/partimaged/partimagedusers || return 1
}
