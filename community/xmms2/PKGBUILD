# $Id: PKGBUILD 17916 2010-05-26 23:17:17Z ibiru $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=xmms2
pkgver=0.7DrNo
pkgrel=5
pkgdesc="complete rewrite of the popular music player"
arch=('i686' 'x86_64')
url="http://xmms2.xmms.org/"
license=('LGPL')
backup=('etc/conf.d/xmms2d.conf')
depends=('sqlite3' 'libmad')
makedepends=('alsa-lib' 'boost' 'curl' 'faad2' 'ffmpeg' 'fftw' 'flac' 'glib' # avahi
	'jack' 'libao' 'libmms' 'libmpcdec' 'libofa' 'libsamplerate' 'libshout'
	'libvorbis' 'libxml2' 'mpg123' 'oss' 'perl' 'pulseaudio' 'pyrex'
	'ruby' 'smbclient' 'speex' 'wavpack' 'libmodplug')
optdepends=('alsa-lib: ALSA audio output'
#	    'avahi: announce xmms2d via bonjour/mDNS/zeroconf'
	    'boost: C++ language bindings'
	    'curl: play HTTP streams'
	    'faad2: AAC support'
	    'ffmpeg: WMA, avcodec & avformat support'
	    'fftw: calculations for visualization'
	    'flac: FLAC support'
	    'jack-audio-connection-kit: JACK audio output'
	    'libao: liboa audio output'
	    'libofa: MusicDNS fingerprinting'
	    'libmms: play MMS streams'
	    'libmodplug: to play MOD files'
	    'libmpcdec: Musepack support'
	    'libsamplerate: vocoder support'
	    'libshout: Icecast audio output'
	    'libvorbis: Ogg Vorbis support'
	    'libxml2: XSPF and podcast support'
	    'mpg123: alternative MP3 plugin'
	    'oss: OSS audio output'
	    'perl: Perl language bindings'
	    'pulseaudio: PulseAudio audio output'
	    'pyrex: Python language bindings'
	    'ruby: Ruby language bindings'
#	    'sidplay: to play a Commodore 64 music files AKA zaks'
	    'smbclient: direct CIFS/SMB access'
	    'speex: Speex support'
	    'wavpack: WavPack support')
conflicts=('xmms2-devel')
install=xmms2.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	xmms2d.conf
	xmms2d.rc
	xmms2-helpers.patch
	xmms2-ruby-1.9.patch)
md5sums=('60e50b591078acb6a85cd83de0f2b077'
         'b3e92b8e2275efc478af775df7a57a13'
         '893371ba4dc7b428322e44329683211f'
         'eddb800ebcc495b8574044a110a34add'
         '017eff634d755c98dd037cd41aecd652')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 < ${srcdir}/xmms2-helpers.patch
# patch -Np1 < ${srcdir}/xmms2-ruby-1.9.patch
#  sed -i -e 's#avcodec.h#libavcodec/avcodec.h#' src/plugins/avcodec/avcodec.c

  ./waf configure --prefix=/usr \
	--with-optionals=python,cli,launcher,xmmsclient++,xmmsclient++-glib,perl,ruby,nycli || return 1
  ./waf build || return 1
  ./waf --destdir=${pkgdir} install || return 1

  install -D -m 0755 ../xmms2d.rc ${pkgdir}/etc/rc.d/xmms2d && \
  install -D -m 0644 ../xmms2d.conf ${pkgdir}/etc/conf.d/xmms2d.conf
}
