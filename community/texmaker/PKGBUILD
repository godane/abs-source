# Contributor: furester <xfurester@hotmail.com> 
# Contributor: Firmicus <firmicus gmx net> 
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=texmaker
pkgver=1.9.9
pkgrel=3
pkgdesc="Free cross-platform latex editor"
arch=('i686' 'x86_64')
url="http://www.xm1math.net/texmaker/index.html"
license=('GPL')
depends=('qt>=4.5.1' 'ghostscript' 'openssl')
source=(http://www.xm1math.net/texmaker/${pkgname}-${pkgver}.tar.bz2 \
	texmaker.desktop)
md5sums=('6a56af78aac811ec3a82e7d375d388ae'
         '48ca54ffd5eb98a83cbf3626205b8639')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	export INSTALL_ROOT=${pkgdir}
	qmake -unix PREFIX=//usr texmaker.pro || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	export INSTALL_PATH=${pkgdir}

	make install || return 1
	
	rm -f ${pkgdir}/usr/share/texmaker/{*.desktop,COPYING}
	install -D -m644 images/appicon.png \
		${pkgdir}/usr/share/pixmaps/texmaker.png
	install -D -m644 ${srcdir}/${pkgname}.desktop \
		${pkgdir}/usr/share/applications/${pkgname}.desktop
}
