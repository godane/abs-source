# Maintainer: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>
# Contributor: kastor <kastor@fobos.org.ar>

pkgname=ushare
pkgver=1.1a
pkgrel=6
pkgdesc="A free UPnP A/V Media Server for Linux"
url="http://ushare.geexbox.org"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libupnp' 'libdlna')
backup=(etc/ushare/ushare.conf)
source=("http://ushare.geexbox.org/releases/${pkgname}-${pkgver}.tar.bz2"
        "ushare")
md5sums=('5bbcdbf1ff85a9710fa3d4e82ccaa251'
         'f252120bcd4362ed64111239d358851c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i -e \
    "s%^INSTALL=\"/usr/bin/install -c\"$%INSTALL=\"$(which install) -c\"%" \
    configure || return 1
  ./configure --prefix=/usr --sysconfdir=/etc/ushare \
    --enable-dlna --with-libdlna-dir=/usr/include/libavformat || return 1

  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  rm -rf ${pkgdir}/etc/${pkgname}/init.d || return 1
  install -m 0755 -D ${srcdir}/ushare ${pkgdir}/etc/rc.d/ushare && \
  install -d -m 0755 ${pkgdir}/etc/conf.d/ && \
  echo "PARAMS=" > ${pkgdir}/etc/conf.d/ushare && \
  install -D -m0644 src/ushare.1 $pkgdir/usr/share/man/man1/ushare.1 || return 1
}
