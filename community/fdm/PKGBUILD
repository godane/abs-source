# $Id: PKGBUILD 14180 2010-04-01 23:59:34Z andrea $
# Maintainer:
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=fdm
pkgver=1.6
pkgrel=2
pkgdesc="A program to fetch and deliver mail"
arch=('i686' 'x86_64')
url="http://fdm.sourceforge.net"
license=('BSD')
depends=('openssl' 'tdb')
options=('!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz"
	'GNUmakefile.patch'
	'LICENSE')
md5sums=('b2b9bfe95217b290142bde1621eb9a37'
         '52955d31121a6365914a0d063be5b32b'
         'b0043d5c7160e5f797048d0d584ef4fa')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i $srcdir/GNUmakefile.patch || return 1

  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

# Examples
  mkdir -p "$pkgdir/usr/share/doc/$pkgname/examples"
  mv "$srcdir/$pkgname-$pkgver/examples" "$pkgdir/usr/share/doc/$pkgname/"
}
