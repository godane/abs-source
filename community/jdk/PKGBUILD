# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=jdk
pkgver=6u20
pkgrel=1
pkgdesc="Sun's Java Development Kit"
arch=('i686' 'x86_64')
  [ "${CARCH}" = "i686" ]   && ARCH=i586
  [ "${CARCH}" = "x86_64" ] && ARCH=amd64
url="http://java.sun.com"
depends=('glibc' 'jre')
install="jdk.install"
source=(http://download.java.net/dlj/binaries/jdk-${pkgver}-dlj-linux-${ARCH}.bin
        https://jdk-distros.dev.java.net/source/browse/*checkout*/jdk-distros/trunk/utils/construct.sh
	jdk.profile
	java-control-panel.desktop 
	java-monitoring-and-management-console.desktop
	java-policy-settings.desktop 
	java-visualvm.desktop 
	java-web-start.desktop)
[ "$CARCH" = "i686" ]   && md5sums=('37afae8ef2481b85b4c9b0356c5d7b40'
			            '94065b612df0046d9ae758943f9f6a75'
				    '7aaac9ec3e440b92361c6561ac00424a'
				    'e4d814c0f310d77ed6990c731bccd0fb'
				    '4bc2b0be93f76ed07b471e3c89eb5a8a'
				    '8e3cec7ab641ec449cabd068be583f5c'
				    '9ba148ebabce8ed3351b813a9a66b6e5'
				    '568451cf45fc8c7adb83a222a61ca2c6')
[ "$CARCH" = "x86_64" ] && md5sums=('e0d03306f57ce1b49d972a21cbb920a0'
			            '94065b612df0046d9ae758943f9f6a75'
	          		    '7aaac9ec3e440b92361c6561ac00424a'
				    'e4d814c0f310d77ed6990c731bccd0fb'
				    '4bc2b0be93f76ed07b471e3c89eb5a8a'
				    '8e3cec7ab641ec449cabd068be583f5c'
				    '9ba148ebabce8ed3351b813a9a66b6e5'
				    '568451cf45fc8c7adb83a222a61ca2c6')
replaces=('j2sdk')
conflicts=('java-environment' 'j2sdk')
provides=('java-environment=6' 'j2sdk')
license=('custom')

build() {
	cd ${srcdir}

	mkdir unbundle-jdk
	cd unbundle-jdk

	sh ../jdk-${pkgver}-dlj-linux-${ARCH}.bin --accept-license

	cd ..

	sh construct.sh unbundle-jdk linux-jdk linux-jre
}

package() {
	cd ${srcdir}

	#copy icon
	install -Dm644 ${srcdir}/linux-jdk/jre/lib/desktop/icons/hicolor/48x48/apps/sun-java.png ${pkgdir}/usr/share/pixmaps/java.png

	rm -rf linux-jdk/jre

	mkdir -p ${pkgdir}/opt
	mv linux-jdk ${pkgdir}/opt/java

	install -D -m755 ${srcdir}/${pkgname}.profile ${pkgdir}/etc/profile.d/${pkgname}.sh

	mkdir -p ${pkgdir}/usr/share/licenses/jdk
	cp ${pkgdir}/opt/java/COPYRIGHT ${pkgdir}/usr/share/licenses/jdk
	cp ${pkgdir}/opt/java/LICENSE ${pkgdir}/usr/share/licenses/jdk
	cp ${pkgdir}/opt/java/THIRDPARTYLICENSEREADME.txt ${pkgdir}/usr/share/licenses/jdk

	#desktop entries
	install -Dm644 ${srcdir}/java-control-panel.desktop ${pkgdir}/usr/share/applications/java-control-panel.desktop
	install -Dm644 ${srcdir}/java-monitoring-and-management-console.desktop  ${pkgdir}/usr/share/applications/java-monitoring-and-management-console.desktop
	install -Dm644 ${srcdir}/java-policy-settings.desktop ${pkgdir}/usr/share/applications/java-policy-settings.desktop
	install -Dm644 ${srcdir}/java-visualvm.desktop ${pkgdir}/usr/share/applications/java-visualvm.desktop
	install -Dm644 ${srcdir}/java-web-start.desktop ${pkgdir}/usr/share/applications/java-web-start.desktop
}
