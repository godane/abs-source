# $Id: PKGBUILD 13230 2010-03-19 17:58:37Z ibiru $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
pkgname=java-gnome
pkgver=4.0.15
pkgrel=1
pkgdesc="Java bindings to the GNOME platform (including gtk, glib and glade)"
arch=('i686' 'x86_64')
url="http://java-gnome.sourceforge.net"
license=('GPL')
depends=('gtksourceview2' 'libnotify' 'libunique' 'gtkspell' 'libglade' 'java-runtime')
makedepends=('java-environment' 'junit')
options=(!libtool)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/4.0/${pkgname}-${pkgver}.tar.gz)
sha256sums=('821c64a38373c723facf69219dcc445e258dd27f8854118eab67d9780c4aba8d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # adapt to the new build-system
  unset CLASSPATH
  if [ -d "/usr/lib/jvm/java-6-openjdk/" ]; then
    ./configure jdk=/usr/lib/jvm/java-6-openjdk/ prefix=/usr || return 1
  else
    ./configure || return 1
  fi
  make || return 1
  DISPLAY= make -j1 doc || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -d ${pkgdir}/usr/share/gtk-doc/html/java-gnome
  cp -R ${srcdir}/java-gnome-${pkgver}/doc/api/* ${pkgdir}/usr/share/gtk-doc/html/java-gnome
}
