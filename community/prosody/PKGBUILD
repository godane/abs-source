# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Dwayne Bent <dbb.0@liqd.org>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>

pkgname=prosody
pkgver=0.7.0rc2
pkgrel=1
pkgdesc="Lightweight and extensible Jabber/XMPP server written in Lua."
arch=('i686' 'x86_64')
url="http://prosody.im/"
license=('MIT')
depends=('lua>=5.1' 'luasocket' 'luaexpat' 'luafilesystem' 'libidn>=0.5.18' 'openssl')
optdepends=('luasec: TLS encryption support'
            'lua-zlib: compression support')
install=prosody.install
backup=('etc/prosody/prosody.cfg.lua'
        'etc/logrotate.d/prosody')
options=(force)
source=("http://prosody.im/depot/${pkgver}/prosody-${pkgver}.tar.gz"
        'prosody.cfg.lua'
        'prosody.rcd'
        'prosody.logrotated')
md5sums=('03c642841aefde86b8652ce3ded263c9'
         'ae255fb5c08718b635af7216029fcc32'
         'f3471e32539c918c0ff2bc07c49f1915'
         '26466fdbea87963a3ca6f48f76fe4a29')

build() {
    cd "$srcdir/prosody-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc/prosody \
        --datadir=/var/lib/prosody || return 1
    make || return 1
    make DESTDIR="$pkgdir" install || return 1

    rm $pkgdir/etc/prosody/certs/*

    install -d "$pkgdir/etc/rc.d" || return 1
    install -d "$pkgdir/etc/logrotate.d" || return 1
    install -d "$pkgdir/var/log/prosody" || return 1
    install -d "$pkgdir/var/run/prosody" || return 1
    install -o root -g root -m 644 "$srcdir/prosody.cfg.lua" \
        "$pkgdir/etc/prosody/prosody.cfg.lua" || return 1
    install -o root -g root -m 755 "$srcdir/prosody.rcd" \
        "$pkgdir/etc/rc.d/prosody" || return 1
    install -o root -g root -m 644 "$srcdir/prosody.logrotated" \
        "$pkgdir/etc/logrotate.d/prosody" || return 1

    install -D -m0644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
