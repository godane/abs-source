# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Sven Kauber, <celeon@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=encfs
pkgver=1.5
pkgrel=11
pkgdesc="Encrypted filesystem in user-space"
arch=('i686' 'x86_64')
url="http://www.arg0.net/encfs"
license=('GPL')
depends=('rlog' 'openssl' 'fuse' 'boost')
options=('!libtool')
source=(http://encfs.googlecode.com/files/${pkgname}-${pkgver}-2.tgz
	encfs-1.5-const.patch
	encfs-1.5-boost-as-needed.patch)
md5sums=('b07008545545b4a57cf2bf65f08a14ad'
         '7f83d584780f985f9a8904c6c2edd3e0'
         'bd8e96dda3ce500e3e2a5c6a47b9fc59')

build(){
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -Np1 -i ${srcdir}/encfs-1.5-const.patch || return 1
	patch -Np1 -i ${srcdir}/encfs-1.5-boost-as-needed.patch || return 1

	export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

	./configure --prefix=/usr
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install
}
