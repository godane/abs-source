# Maintainer: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=qgis
pkgver=1.4.0
pkgrel=2
pkgdesc="A Geographic Information System (GIS) that supports vector, raster & database formats."
url="http://qgis.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('jasper' 'curl' 'qt' 'gsl' 'grass' 'python' 'pyqt' 'giflib' 'xerces-c')
makedepends=('postgis>=1.4' 'netcdf' 'cmake')
options=('force' '!makeflags')
source=(http://download.osgeo.org/qgis/src/${pkgname}_${pkgver}.tar.gz
	qgis.desktop fix-compilation.patch sip.patch)
build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	#patch -Np1 -i ${srcdir}/fix-compilation.patch || return 1
	patch -Np0 -i ${srcdir}/sip.patch

	# Fix insecure RPATH is weird, but just works ;)
        echo "os.system(\"sed -i '/^LFLAGS/s|-Wl,-rpath,.\+ ||g' gui/Makefile core/Makefile\")" >> python/configure.py.in

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DGRASS_PREFIX=/opt/grass \
		-DQGIS_MANUAL_SUBDIR=share/man

	sleep 10
	make || return 1
	make install DESTDIR=${pkgdir}

	# install some freedesktop.org compatibility
	install -D -m644 ${srcdir}/${pkgname}.desktop \
		${pkgdir}/usr/share/applications/${pkgname}.desktop
}
md5sums=('47710e7aa14c2a672c7f28457b0c956f'
         '8ab66039f2aba519b92f52272ec3c13e'
         '3dd026c1491a3515f45854c2908fc7c9'
	 'c1b75ba7b9423a557b0febbe39066286')
