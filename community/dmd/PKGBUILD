# $Id: PKGBUILD 15934 2010-04-18 19:25:31Z cbrannon $
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=dmd
pkgver=1.058
pkgrel=1
pkgdesc="The Digital Mars D compiler."
arch=('i686')
url="http://www.digitalmars.com/d/1.0/"
source=(http://ftp.digitalmars.com/$pkgname.$pkgver.zip)
provides=('d-compiler')
license=('custom')
options=('!strip')
depends=(gcc-libs)
optdepends=('libphobos: D standard runtime library'
            'libtango: Alternative runtime library')

build() {
  # Copy the license.
  install -Dm644 "$srcdir/dmd/license.txt" "$pkgdir/usr/share/licenses/dmd/LICENSE" || return 1

  install -Dm755 "$srcdir/dmd/linux/bin/dmd" "$pkgdir/usr/bin/dmd" || return 1
  install -Dm755 "$srcdir/dmd/linux/bin/dumpobj" "$pkgdir/usr/bin/dumpobj" || return 1
  install -Dm755 "$srcdir/dmd/linux/bin/obj2asm" "$pkgdir/usr/bin/obj2asm" || return 1
  install -Dm755 "$srcdir/dmd/linux/bin/rdmd" "$pkgdir/usr/bin/rdmd" || return 1

  for x in "$srcdir"/dmd/man/man1/*.1; do
    install -Dm644 "$x" "$pkgdir/usr/share/man/man1/$(basename "$x")" || return 1
  done

  for x in "$srcdir"/dmd/man/man1/*.5; do
    install -Dm644 "$x" "$pkgdir/usr/share/man/man5/$(basename "$x")" || return 1
  done
}
md5sums=('33916a9aaadedc18fd3ca6b482b9958c')
