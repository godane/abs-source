# $Id: PKGBUILD 17306 2010-05-22 18:39:33Z heftig $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=pavucontrol
pkgver=0.9.10
pkgrel=2
pkgdesc="A simple GTK based volume control tool for the PulseAudio sound server"
arch=('i686' 'x86_64')
url="http://0pointer.de/lennart/projects/pavucontrol"
license=('GPL')
depends=('gnome-icon-theme' 'libcanberra' 'libglademm' 'pulseaudio')
makedepends=('intltool')
source=(http://0pointer.de/lennart/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz
        pavucontrol.desktop)
md5sums=('b966eb31ec7fd6afa0f1ed7d5ba480b3' '0fc5cb095599eff6915db27e2b9bb881')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-lynx
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/pavucontrol.desktop \
    ${pkgdir}/usr/share/applications/pavucontrol.desktop
}
