# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=puzzles
pkgver=8906
pkgrel=1
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=('i686' 'x86_64')
url="http://www.chiark.greenend.org.uk/~sgtatham/puzzles/"
license=('MIT')
makedepends=('pkgconfig')
depends=('gtk2')
source=(http://www.chiark.greenend.org.uk/~sgtatham/${pkgname}/${pkgname}-r${pkgver}.tar.gz)
md5sums=('de7668abc9d99c28c5f4a56a9e9f7769')

build() {
	cd ${srcdir}/${pkgname}-r${pkgver}

	sed -i 's|\$(gamesdir)|\$(bindir)|' Makefile
	make prefix=/usr || return 1
}

package() {
	cd ${srcdir}/${pkgname}-r${pkgver}

	install -d ${pkgdir}/usr/{bin,share/licenses/${pkgname}}
	install -Dm644 LICENCE ${pkgdir}/usr/share/licenses/${pkgname}/

	make prefix=/usr DESTDIR=${pkgdir} install || return 1

	#fix FS#14600
	mv ${pkgdir}/usr/bin/net ${pkgdir}/usr/bin/puzzles-net || return 1
	mv ${pkgdir}/usr/bin/cube ${pkgdir}/usr/bin/puzzles-cube || return 1
	mv ${pkgdir}/usr/bin/blackbox ${pkgdir}/usr/bin/puzzles-blackbox || return 1
}
