# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=vuze
pkgver=4.4.0.4
pkgrel=1
pkgdesc="BitTorrent client with GUI, written in Java, formerly known as Azureus"
arch=('i686' 'x86_64')
url="http://azureus.sourceforge.net/"
license=('GPL')
depends=('java-runtime')
optdepends=('xulrunner: needed for browsing the vuze content')
provides=('azureus')
conflicts=('azureus' 'java-gcj-compat')
replaces=('azureus')
source=("http://downloads.sourceforge.net/azureus/Vuze_4404_linux.tar.bz2"
	'vuze.desktop')
md5sums=('7fdccc53e5533b13283c624fbcbf21dc'
        '345ad45b972009dcd672c4eaf52fb336')                                     
[ "${CARCH}" = "x86_64" ] && source[0]="http://downloads.sourceforge.net/azureus/Vuze_4404_linux-${CARCH}.tar.bz2"
[ "${CARCH}" = "x86_64" ] && md5sums[0]='0d0f1100c15f78ccb489cfd93f6b862c'

build() {
	/bin/true
}

package() {
	cd ${srcdir}

	install -d ${pkgdir}/usr/share
	cp -r vuze ${pkgdir}/usr/share || return 1

	install -Dm755 vuze/vuze ${pkgdir}/usr/bin/vuze || return 1
	sed -i 's%#PROGRAM_DIR="/home/username/apps/azureus"%PROGRAM_DIR="/usr/share/vuze"%' ${pkgdir}/usr/bin/vuze || return 1

	install -Dm644 vuze/vuze.png ${pkgdir}/usr/share/pixmaps/vuze.png || return 1
	install -Dm644 vuze.desktop  ${pkgdir}/usr/share/applications/vuze.desktop || return 1
}
