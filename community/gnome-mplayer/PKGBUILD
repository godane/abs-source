# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gnome-mplayer
pkgver=0.9.9.2
pkgrel=1
pkgdesc="A simple MPlayer GUI"
arch=('i686' 'x86_64')
url="http://gnome-mplayer.googlecode.com/"
license=('GPL')
depends=('mplayer' 'dbus-glib' 'libnotify' 'libgpod')
install=${pkgname}.install
source=(http://gnome-mplayer.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('79810913d245755b60c27d37e1d1f178')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/usr --sysconfdir=/etc \
		--disable-schemas-install --without-gconf || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	sed -i 's@Exec=gnome-mplayer %U@Exec=gnome-mplayer %F@' ${pkgdir}/usr/share/applications/${pkgname}.desktop || return 1
}
