# $Id: PKGBUILD 17099 2010-05-19 11:10:11Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=opensips
pkgver=1.6.2
pkgrel=2
pkgdesc="An Open Source SIP Server able to act as a SIP proxy, registrar, location server, redirect server ..."
url="http://www.opensips.org"
depends=('gcc-libs' 'openssl' 'db' 'attr' 'kernel-headers' 'libxml2')
makedepends=('postgresql-libs>=8.4.1' 'unixodbc' 'libldap>=2.4.18' 'libmysqlclient')
optdepends=('postgresql-libs' 'unixodbc' 'libldap' 'libmysqlclient' 'libsasl' 'python')
backup=("etc/opensips/opensips.cfg" \
	"etc/opensips/dictionary.radius" \
	"etc/opensips/opensipsctlrc")
arch=('i686' 'x86_64')
license=('GPL')
install=opensips.install
options=('!emptydirs' 'zipman' '!makeflags' 'docs')
source=(http://switch.dl.sourceforge.net/sourceforge/opensips/opensips-$pkgver-tls_src.tar.gz \
	opensips.init)
md5sums=('9565e33cca773af0ba3d2d33213270b9'
         '685fbe00826df1285b410d4610dcbb0c')

build()
{
  cd $srcdir/$pkgname-$pkgver-tls/

  make \
	include_modules="ldap db_mysql db_postgres db_unixodbc presence presence_xml h350" \
	TLS=1 DESTDIR=$pkgdir/usr LIBDIR=lib install || return 1

  # Conforms to the arch packaging standards (http://wiki.archlinux.org/index.php/Arch_Packaging_Standards)
  mkdir -p $pkgdir/etc/ && \
  mv $pkgdir/usr/etc/opensips/ $pkgdir/etc/ && \
  sed -i 's#mpath=".*lib/opensips/modules/"#mpath="/usr/lib/opensips/modules/"#' $pkgdir/etc/opensips/opensips.cfg || return 1

  # Install starting script
  mkdir -p $pkgdir/etc/rc.d/ && \
  cp ../opensips.init $pkgdir/etc/rc.d/opensips && \
  chmod 755 $pkgdir/etc/rc.d/opensips || return 1

  # fix bad paths
  cd $pkgdir/usr/share && \
  find -type f -exec sed -i "s#$pkgdir##" {} \; || return 1

  cd $pkgdir/usr/lib/opensips/opensipsctl && \
  find -type f -exec sed -i "s#$pkgdir##" {} \; || return 1

  cd $pkgdir/usr/sbin && \
  sed -i "s#$pkgdir##" opensipsctl opensipsdbctl osipsconsole || return 1

  cd $pkgdir/etc && \
  find -type f -exec sed -i "s#$pkgdir##" {} \; || return 1
}
