# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=pinot
pkgver=0.95
pkgrel=3
pkgdesc="Personal search and metasearch tool"
arch=('i686' 'x86_64')
url="http://pinot.berlios.de/"
license=('GPL')
depends=('gtkmm' 'xapian-core' 'libtextcat' 'sqlite3' 'libxml++' 'curl'
         'gmime' 'dbus-glib' 'shared-mime-info' 'libexif' 'taglib' 
         'hicolor-icon-theme' 'cairo')
makedepends=('boost' 'desktop-file-utils')
optdepends=('unzip: ZIP files extraction'
            'poppler: PDF to text conversion'
            'catdvi: DVI to text conversion'
            'djvulibre: DjVu text extraction'
            'unrtf: RTF to HTML conversion'
            'antiword: MS Word to text conversion'
            'catdoc: XLS and PPT to text conversion'
            'deskbar-applet: Pinot Deskbar-Applet module')
install=pinot.install
source=(http://download.berlios.de/pinot/pinot-${pkgver}.tar.gz)
md5sums=('873e34082e3cbe32b7fea3598c1c0514')

build() {
	cd ${srcdir}/pinot-${pkgver}

	./configure --prefix=/usr --sysconfdir=/etc || return 1
	make || return 1
}

package() {
	cd ${srcdir}/pinot-${pkgver}

	make DESTDIR=${pkgdir} install || return 1

	# Remove Deskbar-Applet handler
	rm -rf ${pkgdir}/usr/lib/deskbar-applet/handlers || return 1
}
