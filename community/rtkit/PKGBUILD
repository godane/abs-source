# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
pkgname=rtkit
pkgver=0.7
pkgrel=2
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=('i686' 'x86_64')
url="http://git.0pointer.de/?p=rtkit.git"
license=('GPL' 'custom:BSD')
depends=('dbus' 'polkit')
makedepends=('pkgconfig')
install=rtkit.install
source=(http://0pointer.de/public/$pkgname-$pkgver.tar.gz)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --libexecdir=/usr/lib --prefix=/usr --sysconfdir=/etc
  make || return 1
  make DESTDIR="$pkgdir/" install

  # DBus configuration
  install -dm755 "$pkgdir/usr/share/dbus-1/interfaces/"
  $srcdir/$pkgname-$pkgver/rtkit-daemon --introspect \
    >$pkgdir/usr/share/dbus-1/interfaces/org.freedesktop.RealtimeKit1.xml

  # Client license
  install -dm755 "$pkgdir/usr/share/licenses/rtkit/"
  head -24 "$srcdir/$pkgname-$pkgver/rtkit.c" | tail -21 \
    >"$pkgdir/usr/share/licenses/rtkit/COPYING"
}

md5sums=('7da26aad801e92760f11e98bfa3fc961')
