# $Id: PKGBUILD 11649 2010-02-15 16:35:32Z ibiru $
# Maintainer: Ionut Biru  <ibiru@archlinux.ro>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=0.18.1
pkgrel=1
pkgdesc="A fast and lightweight IDE"
arch=('i686' 'x86_64')
url="http://www.geany.org"
license=('GPL')
depends=('gtk2>=2.8.0')
makedepends=('perlxml' 'intltool')
optdepends=("vte: for terminal support")
install=geany.install
source=(http://download.geany.org/$pkgname-$pkgver.tar.gz)
options=('!libtool')
md5sums=('bd1f70a5ee9a7f10c3c4f17bd263a0b1')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr
  make || return 1

  # FS#10318
  sed -i 's|MimeType=text/plain;|MimeType=|' geany.desktop || return 1
  # Add colors for PKGBUILD file
  sed -i 's|Sh=|Sh=PKGBUILD;|' data/filetype_extensions.conf || return 1

  make DESTDIR=${pkgdir} install || return 1
}
