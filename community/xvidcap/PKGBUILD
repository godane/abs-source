# $Id: PKGBUILD 63169 2010-01-15 14:58:38Z giovanni $
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: red_over_blue, Abaddon <kukububu@go2.pl>
# Contributor: damir <damir@archlinux.org>

pkgname=xvidcap
pkgver=1.1.7
pkgrel=3
pkgdesc="A small tool to capture things going on on an X-Windows display to either individual frames or an MPEG video"
arch=("i686" "x86_64")
license=('GPL')
url="http://xvidcap.sourceforge.net/"
depends=('dbus-glib' 'lame' 'libxmu' 'libglade' 'ffmpeg')
#ffmpeg not needed yet, because internal one used
makedepends=('perlxml' 'rarian')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	'xextproto-7.1.1.patch'
	'xvidcap-ffmpeg.patch')
md5sums=('b39a682d3ef9fcbf424af771936780e2'
	'fbde2b076c5ea05723883b87f3ef2a65'
	'f18817da1c0dada05fa791ae53193b2c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 -i ${srcdir}/xextproto-7.1.1.patch
  patch -Np0 -i ${srcdir}/xvidcap-ffmpeg.patch || return 1

  ./configure --prefix=/usr \
    --with-x \
    --with-gtk2 \
    --enable-libmp3lame \
    --without-forced-embedded-ffmpeg
  make || return 1
  make prefix=${pkgdir}/usr install || return 1
}
