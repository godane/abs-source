# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb ][ gmail ? com>
# Contributor: Jonathan Schaeffer <joschaeffer ][ gmail ? com>
# Contributor: chfoxli <lg ][ romandie ? com>

pkgname=kphotoalbum
pkgver=4.1.1
pkgrel=5
pkgdesc="KDE Photo Album, picture collection manager"
url="http://www.kphotoalbum.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdebase-runtime' 'kdegraphics-libs' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4' 'kdeedu-marble' 'kipi-plugins' 'kdemultimedia-mplayerthumbs')
optdepends=('kdeedu-marble: integration with Marble'
	'kipi-plugins: add extra functionality'
	'kdemultimedia-mplayerthumbs: video thumbnails')
install=${pkgname}.install
source=("http://www.kphotoalbum.org/data/download/${pkgname}-${pkgver}.tar.bz2")
md5sums=('c00a5f179e487ac89fccf8dfb3cd6ef0')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
