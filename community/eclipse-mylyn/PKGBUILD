# $Id: PKGBUILD 15728 2010-04-14 17:54:25Z angvp $
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=eclipse-mylyn
pkgver=3.3.3
pkgrel=1
pkgdesc="A task-focused interface for Eclipse"
arch=('any')
url="http://www.eclipse.org/mylyn/"
license=('EPL')
depends=('eclipse')
makedepends=('unzip')
optdepends=('bugzilla: ticketing support')
source=(http://download.eclipse.org/tools/mylyn/update/mylyn-${pkgver}-e3.4.zip)
md5sums=('c161200c4defd6b669732911712c57fd')

build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
      cd - >/dev/null 2>&1
    elif [[ ! ${_feature} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_feature%\.pack\.gz$} ] ; then
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    if [[ ! ${_plugin} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_plugin%\.pack\.gz$} ] ; then
      install -Dm644 ${_plugin} ${_dest}/${_plugin}
    fi
  done
}

