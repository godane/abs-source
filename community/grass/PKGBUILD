# $Id: PKGBUILD 11630 2010-02-15 15:45:52Z ibiru $
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org

pkgname=grass
pkgver=6.4.0RC5
pkgrel=4
pkgdesc="Geographic Information System (GIS) used for geospatial data management and analysis, \
	image processing, graphics/maps production, spatial modeling, and visualization"
arch=('i686' 'x86_64')
url="http://grass.itc.it/index.php"
license=('GPL')
depends=('gdal' 'tk' 'sqlite3' 'xorg-server' 'python' 'mesa' 'swig' 'proj' 'libjpeg>=8' 'libpng>=1.4.0' 'libtiff')
makedepends=('mysql' 'postgresql>=8.4.1' 'r' 'fftw' 'freetype2')
optdepends=('fftw: required for i.fft and i.ifft modules'
'postgresql: PostgreSQL database interface'
'r: R language interface'
'lapack: required for GMATH library'
'blas: required for GMATH library')
options=('!libtool' '!makeflags')
install=grass.install
source=(http://grass.itc.it/grass64/source/$pkgname-$pkgver.tar.gz $pkgname.desktop grass.sh grass.png)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/opt --with-mysql-includes=/usr/include/mysql \
    --with-mysql --with-fftw --with-gdal=/usr/bin/gdal-config --with-sqlite --with-python --with-blas --with-lapack --with-postgres \
    --with-proj-libs=/usr/lib --with-proj-includes=/usr/include \
    --with-proj-share=/usr/share/proj \
    --with-fftw-includes=/usr/include --with-fftw-libs=/usr/lib || return 1
    #--with-freetype-includes=/usr/include

  make || return 1
  make INST_DIR=$pkgdir/opt/grass-$pkgver BINDIR=$pkgdir/usr/bin         install || return 1

  # fix $GISBASE path
  sed -i "s|GISBASE=$pkgdir/opt/grass-$pkgver|GISBASE=/opt/grass-$pkgver|g" "$pkgdir/usr/bin/grass64" || return 1

  # strip html docs
  rm -r "$pkgdir/opt/grass-$pkgver/docs/html"

  # install profile.d file
  install -D -m755 "$srcdir/grass.sh" "$pkgdir/etc/profile.d/grass.sh" || return 1

  # install some freedesktop.org compatibility
  install -D -m644 "$srcdir/$pkgname.desktop" \
      "$pkgdir/usr/share/applications/$pkgname.desktop" || return 1

  install -D -m644 "$srcdir/$pkgname.png" \
        "$pkgdir/usr/share/pixmaps/$pkgname.png" || return 1

  # create a symlink for version work around
  ln -sf /opt/grass-$pkgver "$pkgdir/opt/grass"

  install -d -m755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/opt/grass/lib' > ${pkgdir}/etc/ld.so.conf.d/grass.conf
}
md5sums=('4689a50c5fd66c1e0493ec127081e142'
         'c0d669c77f60f8544a34a55c5e9e087c'
         'db93730dd330f1539bd31b4373e1c00e'
         'ae778ed261a40b078a8465994a0eb25e')
