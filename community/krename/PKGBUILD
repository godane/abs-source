# $Id: PKGBUILD 12548 2010-03-08 00:21:39Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: FUBAR

pkgname=krename
pkgver=4.0.3
pkgrel=2
pkgdesc="A very powerfull batch file renamer for KDE"
arch=('i686' 'x86_64')
url="http://www.krename.net"
license=('GPL')
depends=('kdelibs' 'taglib' 'xdg-utils')
makedepends=('cmake' 'automoc4')
options=('libtool')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('a8e2a4f8bb28187ef126658b35478575')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release || return 1
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
