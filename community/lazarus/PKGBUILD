# $Id: PKGBUILD 17940 2010-05-27 08:41:38Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Adam (byte/jra) <j_adam@web.de>

pkgname=lazarus
pkgver=0.9.29
_snapshot=25678-20100527
pkgrel=1
pkgdesc='Delphi-like IDE for FreePascal'
url='http://www.lazarus.freepascal.org/'
license=('GPL2' 'MPL' 'custom:LGPL')
arch=('i686' 'x86_64')
depends=('fpc' 'fpc-src' 'gdk-pixbuf' 'gtk2')
makedepends=(rpmextract)
options=('!emptydirs' '!makeflags')
source=(http://snapshots.lazarus.shikami.org/files/lazarus-$pkgver.${_snapshot}.src.rpm)
md5sums=('afda29734c84c7d4aad24d199723e4ed')

build() {
  [ $NOEXTRACT -eq 1  ] || rpmextract.sh lazarus-$pkgver.${_snapshot}.src.rpm
  [ $NOEXTRACT -eq 1  ] || tar xzf lazarus-$pkgver.${_snapshot}.tar.gz

  cd $srcdir/${pkgname}
  # don't 'make examples' or you'll end up with a 100MB pkg
  make FPC=/usr/bin/fpc clean all || return 1

  # skip the 'make install' mess completely and do everything manually
  mkdir -p $pkgdir/usr/lib/lazarus $pkgdir/usr/bin $pkgdir/usr/share/man/man1 $pkgdir/usr/share/doc
  rm -r debian
  cp -R . $pkgdir/usr/lib/lazarus || return 1

  ln -s /usr/lib/lazarus/lazarus $pkgdir/usr/bin/lazarus && \
  ln -s /usr/lib/lazarus/startlazarus $pkgdir/usr/bin/startlazarus && \
  ln -s /usr/lib/lazarus/lazbuild $pkgdir/usr/bin/lazbuild || return 1

  cp -R install/man/man1/* $pkgdir/usr/share/man/man1/ || return 1

  mv $pkgdir/usr/lib/lazarus/docs $pkgdir/usr/share/doc/lazarus || return 1

  # make 'desktop-file-validate' happy and fix missing .png icon
  sed -e 's|\(Categories\).*|\1=IDE;Development;|' \
      -e 's|\.png|\.xpm|' -i install/lazarus.desktop && \
  install -Dm644 install/lazarus.desktop $pkgdir/usr/share/applications/lazarus.desktop && \
  install -Dm644 images/ide_icon48x48.png $pkgdir/usr/share/pixmaps/lazarus.png && \
  rm -r $pkgdir/usr/lib/lazarus/install || return 1

  # license files: /usr/lib/lazarus/COPYING*
  install -D -m644 COPYING.modifiedLGPL.txt $pkgdir/usr/share/licenses/$pkgname/COPYING.modifiedLGPL || return 1

  # strip
  find $pkgdir -perm /ugo+x -type f -exec strip {} \; && \
  find $pkgdir -name \*.so -type f -exec strip {} \;
}
