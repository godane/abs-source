# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=acetoneiso2
pkgver=2.2.1
pkgrel=4
pkgdesc="An all in one ISO tool (bin mdf nrg img daa dmg cdi b5i bwi pdi iso)"
arch=('i686' 'x86_64')
url="http://www.acetoneteam.org"
license=('GPL3')
depends=('qt>=4' 'fuseiso' 'cdrdao' 'cdrkit' 'gnupg2' 'pinentry' 'mplayer' 'phonon')
optdepends=("p7zip: for ISO compress/decompress support"
            "dvd+rw-tools: for dvd ISO creation")
conflicts=('acetoneiso')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/acetoneiso/AcetoneISO/AcetoneISO%202.2.1/acetoneiso_${pkgver}.tar.gz)
md5sums=('c4891fb1ddfe9d3dff4b55e8ea27ed29')

build() {
	cd ${srcdir}/acetoneiso_${pkgver}/acetoneiso

	qmake
	sed -i 's|-I/usr/include/QtCore|-I/usr/include/QtCore -I/usr/include/KDE|' Makefile
	make || return 1
}

package() {
	cd ${srcdir}/acetoneiso_${pkgver}/acetoneiso

	make INSTALL_ROOT="${pkgdir}" install
}
