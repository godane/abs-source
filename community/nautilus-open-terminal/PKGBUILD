# $Id: PKGBUILD 13947 2010-03-31 09:57:09Z ibiru $
# Maintainer : Biru Ionut <ionut@archlinux.ro>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
pkgname=nautilus-open-terminal
pkgver=0.18
pkgrel=2
pkgdesc="A nautilus plugin for opening terminals in arbitrary local paths"
arch=('i686' 'x86_64')
url="http://manny.cluecoder.org/packages/nautilus-open-terminal"
license=('GPL')
depends=('nautilus')
makedepends=('pkgconfig' 'gettext' 'intltool')
install=nautilus-open-terminal.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/${pkgname}-${pkgver}.tar.gz)
options=('!libtool')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install || return 1

  install -dm755 "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" \
                     --domain $pkgname \
                     "$pkgdir"/etc/gconf/schemas/*.schemas || return 1
  rm -rf "$pkgdir"/etc/gconf/schemas
}

md5sums=('c0718d28e6d9984c0bdf92c30ff72d49')
