# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Leslie P. Polzer <polzerΘgnu·org>
# Contributor: erm67 <erm67Θyahoo·it>
# Maintainer: Daniel J Griffiths

pkgname=djview4
pkgver=4.5
pkgrel=4
pkgdesc="DJVU viewer (Qt4 version)"
arch=('i686' 'x86_64')
url="http://djvu.sourceforge.net/"
license=('GPL')
depends=('qt>=4.1' 'openssl' 'libdjvu>=3.5.19' 'xdg-utils')
install=djview4.install
source=("http://downloads.sourceforge.net/djvu/${pkgname}-${pkgver}.tar.gz")
md5sums=('0461a26dbcac33dcc5eb7955ac357504')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	QTDIR=/usr ./configure --prefix=/usr --disable-nsdejavu || return 1
	make || return 1
}

package() {
	desktopdir='/usr/share/djvu/djview4/desktop'
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR="${pkgdir}" LN_S=false install

	install -Dm644 ${pkgdir}${desktopdir}/hi32-djview4.png \
		${pkgdir}/usr/share/icons/hicolor/32x32/apps/djvulibre-djview4.png
	install -Dm644 ${pkgdir}${desktopdir}/hi64-djview4.png \
		${pkgdir}/usr/share/icons/hicolor/64x64/apps/djvulibre-djview4.png
	install -Dm644 ${pkgdir}${desktopdir}/hi64-djview4.png \
		${pkgdir}/usr/share/pixmaps/djvulibre-djview4.png
	install -Dm644 ${pkgdir}${desktopdir}/djvulibre-djview4.desktop \
		${pkgdir}/usr/share/applications/djvulibre-djview4.desktop
	rm -R ${pkgdir}${desktopdir}
}
