# $Id: PKGBUILD 15579 2010-04-12 21:27:38Z ibiru $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>
pkgbase=uzbl
pkgname=('uzbl-core' 'uzbl-browser' 'uzbl-tabbed')
pkgver=2010.04.03
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.uzbl.org"
license=('GPL3')
makedepends=('git' 'pkgconfig' 'libwebkit>=1.2.0')
source=(uzbl-$pkgver.tar.gz::http://github.com/Dieterbe/uzbl/tarball/$pkgver)
sha256sums=('3aa1412dba013a8db0261b2c0773c94e121aeb98c9d03dce4ea049329f4abacb')
build() {
    cd "$srcdir"/Dieterbe-uzbl-*
    make || return 1
}
package_uzbl-core(){
   pkgdesc="Webpage interface component meant for integration with other tools and scripts"
   depends=('libwebkit>=1.2.0')
   optdepends=('socat: to interface with the socket' 'dmenu: to run some of the example scripts'
               'zenity: to run some of the example scripts'
               'python: to run some of the example scripts' 'perl: for the example formfiller'
               'xclip: for clipboard related keybindings' 'pygtk: for uzbl-tabbed')

   cd "$srcdir"/Dieterbe-uzbl-*
   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-core || return 1
}

package_uzbl-browser(){
   pkgdesc="A complete browser experience based on uzbl-core"
   depends=("uzbl-core=$pkgver" 'socat' 'zenity' 'xclip' 'dmenu' 'python' 'perl')
   cd "$srcdir"/Dieterbe-uzbl-*
   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-browser || return 1
}
package_uzbl-tabbed(){
   pkgdesc="Tabbing manager providing multiple uzbl-browser instances in 1 window"
   depends=("uzbl-browser=$pkgver" 'pygtk')
   cd "$srcdir"/Dieterbe-uzbl-*
   make DESTDIR="$pkgdir" PREFIX=/usr install-uzbl-tabbed || return 1
}

