# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=recoll
pkgver=1.13.04
pkgrel=2
pkgdesc="Full text search tool based on Xapian backend"
arch=('i686' 'x86_64')
url="http://www.lesbonscomptes.com/recoll/"
license=('GPL')
depends=('xapian-core>=1.0.15-1' 'qt' 'openssl' 'hicolor-icon-theme')
optdepends=('unzip: for the OpenOffice.org documents'
	    'xpdf: for pdf'
	    'pstotext: for postscipt'
	    'antiword: for msword'
	    'catdoc: for ms excel and powerpoint'
	    'unrtf: for RTF'
	    'tetex: for dvi support with dvips'
	    'djvulibre: for djvu'
	    'id3lib: for mp3 tags support with id3info')
install=recoll.install
source=(http://www.lesbonscomptes.com/${pkgname}/${pkgname}-${pkgver}.tar.gz
	recoll-1.13.04-xapian-1.2.patch)
md5sums=('eba9d639dbac149996a92b9b2dffc5ee'
	 '2f2a9b23fe29d64d8223abe33a255da3')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -p1 < ${srcdir}/recoll-1.13.04-xapian-1.2.patch || return 1
	./configure --prefix=/usr --mandir=/usr/share/man
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install
}
