# $Id: PKGBUILD 12607 2010-03-09 10:53:54Z andrea $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=qtpfsgui
pkgver=1.9.3
pkgrel=5
pkgdesc="Qt4 graphical user interface that provides a workflow for HDR imaging"
arch=('i686' 'x86_64')
url="http://qtpfsgui.sourceforge.net/"
license=('GPL')
depends=('openexr' 'exiv2' 'fftw' 'hicolor-icon-theme' 'libtiff' 'qt')
optdepends=('dcraw: work with raw-files')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('5a6421391e373c912e4a793e131151c8')

build() {
  cd ${srcdir}/$pkgname-$pkgver
# FS#16795
  sed -i -e '/QMAKE_CXXFLAGS/d' project.pro
# translations
  lrelease project.pro

  qmake PREFIX=/usr
  make || return 1
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  make INSTALL_ROOT=${pkgdir} install
}
