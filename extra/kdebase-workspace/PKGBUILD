# $Id: PKGBUILD 59826 2009-11-28 01:40:21Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace
pkgver=4.4.4
pkgrel=1
pkgdesc="KDE Base Workspace"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
# note on libxdamage:
# 	not detected by namcap because libgl depends on it
#	but nvidia providing libgl does not depend on libxdamage
depends=('polkit-kde' 'kdepimlibs' 'kdepim-runtime' 'kdebase-runtime' 'qimageblitz'
         'libxxf86misc' 'libxcomposite' 'libxss' 'lm_sensors' 'libxklavier' 'libxft'
         'libxdamage' 'xorg-utils' 'libxrandr')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'networkmanager' 'bluez' 'kdebindings-python'
             'kdebindings-ruby')
replaces=('kdmtheme' 'kde-common' 'guidance-power-manager' 'policykit-kde')
conflicts=('kde-common' 'guidance-power-manager' 'policykit-kde')
install='kdebase-workspace.install'
backup=('usr/share/config/kdm/kdmrc'
        'etc/pam.d/kde'
        'etc/pam.d/kde-np'
        'etc/pam.d/kscreensaver')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kdm-zsh-profile.patch' 'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam'
        'fixpath.patch' 'terminate-server.patch' 'screensaver-sleep.patch')
sha1sums=('7b20bc91a5969b4c42c83076926cf6838b2ba47b'
          '8c2bdefb23a03b753b78d16944d03fa3939d2d99'
          '5db3a245201bd4a50e65aa2ef583cf5490e4f646'
          'f7b38af38549242a240f1a90ab9964ca8a366129'
          '603cc79c4d2b4eae62bb5f244aeecb3a778b5516'
          '106635aa1aae51d6f0668b1853f6c49a4fe9d3d8'
          'd7b5883f7e65c6839b1f65f94d58026673dd0226'
          'ac7bc292c865bc1ab8c02e6341aa7aeaf1a3eeee'
          'd676932e89f201d1f381615651761af1badb88be')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -p0 -i ${srcdir}/kdm-zsh-profile.patch || return 1
	patch -p0 -i ${srcdir}/fixpath.patch || return 1
	patch -p0 -i ${srcdir}/terminate-server.patch || return 1
        patch -p1 -i ${srcdir}/screensaver-sleep.patch || return 1 # FS#19426      

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF \
		-DWITH_Googlegadgets=OFF \
		-DWITH_QEdje=OFF \
		-DWITH_PolkitQt=OFF
	make || return 1
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install

	install -D -m755 ${srcdir}/kdm ${pkgdir}/etc/rc.d/kdm
	install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
	install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
	install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
	install -d -m755 ${pkgdir}/etc/X11/sessions/
	ln -sf /usr/share/apps/kdm/sessions/kde{,-safe}.desktop ${pkgdir}/etc/X11/sessions/
	install -d -m755 ${pkgdir}/etc/kde/{env,shutdown}
}
