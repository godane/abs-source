# $Id: PKGBUILD 63407 2010-01-17 13:34:04Z jgc $
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Mantainer: Roberto Carvajal <roberto@archlinux.org>

pkgname=mjpegtools
pkgver=1.9.0
pkgrel=3
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video"
arch=(i686 x86_64)
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=('libjpeg>=8' 'libpng>=1.4.0' 'sdl' 'gcc-libs' 'libdv')
makedepends=('gtk2>=2.18.6')
optdepends=('gtk2: glav GUI')
options=('!makeflags' '!libtool')
source=(http://downloads.sourceforge.net/sourceforge/mjpeg/${pkgname}-${pkgver}.tar.gz
        mjpegtools-1.9.0-glibc-2.10.patch
        png2yuv-fix-memleak.patch)
md5sums=('309a6fcf0900a010d6a9c1e91afc2f5c'
         'b8ae66237d83be533db8eea166fd3357'
         '39e1def8fb0f7c58a217b22dc251a86a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e '/ARCHFLAGS=/s:=.*:=:' configure || return 1
  patch -Np1 -i "$srcdir/mjpegtools-1.9.0-glibc-2.10.patch" || return 1
  patch -Np1 -i "${srcdir}/png2yuv-fix-memleak.patch" || return 1
  ./configure --prefix=/usr --enable-largefile || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m644 mpeg2enc/mpeg2syntaxcodes.h \
      "${pkgdir}/usr/include/mjpegtools/mpeg2enc/" || return 1
}
