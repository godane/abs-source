# $Id: PKGBUILD 59737 2009-11-27 03:57:04Z paul $
# Maintainer: Paul Mattal <paul.archlinux.org>

pkgname=truecrypt
pkgver=6.3a
pkgrel=1
pkgdesc="Free open-source cross-platform disk encryption software"
url="http://www.truecrypt.org/"
arch=('i686' 'x86_64')
license=('custom') # TrueCrypt License v2.6
depends=('fuse>=2.8.0' 'wxgtk>=2.8.9' 'libsm' 'device-mapper')
optdepends=('sudo: mounting encrypted volumes as nonroot users')
conflicts=('truecrypt-utils')
replaces=('truecrypt-utils')
options=('force')
# N.B. Truecrypt's web-based source download is incompatible with
# makepkg. Source has been placed on ftp.archlinux.org instead
source=("ftp://ftp.archlinux.org/other/tc/${pkgname}-${pkgver}.tar.gz" \
        'ftp://ftp.archlinux.org/other/tc/pkcs-2.20.tar.gz' \
        "${pkgname}.desktop") 

build() {
  export PKCS11_INC="${srcdir}/pkcs-2.20"

  cd ${srcdir}/${pkgname}-${pkgver}-source || return 1
  make || return 1

  # Install Binary
  install -D -m755 Main/${pkgname} ${pkgdir}/usr/bin/${pkgname} || return 1

  # Install Desktop file and Icon
  install -D -m644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop || return 1
  install -D -m644 Resources/Icons/TrueCrypt-48x48.xpm ${pkgdir}/usr/share/pixmaps/truecrypt.xpm || return 1

  # Install License
  install -D -m644 License.txt ${pkgdir}/usr/share/licenses/${pkgname}/License.txt || return 1
}
md5sums=('0a7cc9a340df0aa0a86e8f2ff3fdda92'
         'ce6a707b79411e82e8e558aa03e764b0'
         '183b9bf04a943454d4e216130bcbac01')
