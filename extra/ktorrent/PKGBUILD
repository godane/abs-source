# $Id: PKGBUILD 72842 2010-03-20 13:36:03Z pierre $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=3.3.4
pkgrel=2
pkgdesc="A BitTorrent program for KDE"
arch=('i686' 'x86_64')
url="http://ktorrent.org"
license=('GPL2')
depends=('kdelibs' 'gmp' 'qca' 'libxft' 'kdebase-runtime' 'taglib' 'hicolor-icon-theme')
# 'kdebase-runtime' is needed for some of the icons in the interface to show
makedepends=('autoconf' 'perl' 'automoc4' 'cmake' 'kdepimlibs' 'kdebase-workspace')
# 'kdebase-workspace' is needed to enable plasmoid building
source=("http://ktorrent.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
	'httpserver.patch')
install=ktorrent.install
options=('libtool')
md5sums=('7d7dba06a78d09494199bd4ef82e5527'
         '1f25b94615b0bbfa0457c99fb20de3e6')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# build time patch
	patch -Np0 -i ${srcdir}/httpserver.patch || return 1

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_SKIP_RPATH=ON \
	  -DENABLE_MEDIAPLAYER_PLUGIN=false
	make || return 1
}

package() {
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install
}
