# $Id: PKGBUILD 76027 2010-04-06 09:28:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevplatform
pkgver=0.10.0
pkgrel=1
pkgdesc="A C/C++ development platform for KDE"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdelibs' 'boost' 'subversion' 'commoncpp2')
optdepends=("kdesdk-kompare: difference checking")
makedepends=('cmake' 'automoc4')
install=kdevplatform.install
source=("http://download.kde.org/unstable/kdevelop/3.10.0/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('e71de75821cdb5141e9e4a88c5bd6bf4')

build() {
  cd ${srcdir}
  mkdir build
  cd build 
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
