# $Id: PKGBUILD 75602 2010-04-01 22:50:54Z giovanni $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=vsftpd
pkgver=2.2.2
pkgrel=3
pkgdesc="Very Secure FTP daemon"
arch=('i686' 'x86_64')
license=('GPL')
backup=(etc/vsftpd.conf etc/xinetd.d/vsftpd)
url="http://vsftpd.beasts.org/"
makedepends=('vim')
depends=('glibc' 'pam' 'tcp_wrappers' 'openssl' 'libcap')
install=vsftpd.install
source=(ftp://vsftpd.beasts.org/users/cevans/vsftpd-$pkgver.tar.gz \
        vsftpd.xinetd vsftpd.d)
md5sums=('6d6bc136af14c23f8fef6f1a51f55418'
         'b07fd4609c70063c1d6b20142910c1a6'
         '5cf6effd8b1603fa809eadf11bd696bd')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_TCPWRAPPERS$|#define VSF_BUILD_TCPWRAPPERS|' \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h
  make CFLAGS="${CFLAGS}" || return 1
  install -D -m755 vsftpd ${pkgdir}/usr/sbin/vsftpd || return 1
  install -D -m644 vsftpd.conf ${pkgdir}/etc/vsftpd.conf || return 1
  install -D -m644 vsftpd.8 ${pkgdir}/usr/share/man/man8/vsftpd.8 || return 1
  install -D -m644 vsftpd.conf.5 ${pkgdir}/usr/share/man/man5/vsftpd.conf.5 || return 1
  install -D -m644 ../vsftpd.xinetd ${pkgdir}/etc/xinetd.d/vsftpd  || return 1
  install -D -m755 ../vsftpd.d ${pkgdir}/etc/rc.d/vsftpd || return 1
  mkdir -p $startdir/pkg/usr/share/empty
  ex ${pkgdir}/etc/vsftpd.conf <<_EOF
2
i
#
# Use this to use vsftpd in standalone mode, otherwise it runs through (x)inetd
#listen=YES
.
w
q
_EOF
}
