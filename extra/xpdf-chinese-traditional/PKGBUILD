# $Id: PKGBUILD 59100 2009-11-20 06:51:36Z allan $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=xpdf-chinese-traditional
pkgver=20040727
pkgrel=4
pkgdesc="Adds support for traditional Chinese fonts to xpdf"
arch=('any')
url="http://www.foolabs.com/xpdf/"
license=('GPL2' 'custom')
depends=('xpdf')
install=$pkgname.install
source=(ftp://ftp.foolabs.com/pub/xpdf/$pkgname.tar.gz LICENSE)
md5sums=('b7fa7f1850f7cfa28498701507079abb' 'c712b6980d92a9726e5f6638389189e0')

build() {
  cd $srcdir/$pkgname
  install -dm755 $pkgdir/usr/share/xpdf/CMap
  install -m644 -t $pkgdir/usr/share/xpdf *.unicodeMap
  install -m644 -t $pkgdir/usr/share/xpdf/CMap CMap/*

  # relocate language specific files
  sed -i 's|/usr/local/share/xpdf/chinese-traditional|/usr/share/xpdf|' add-to-xpdfrc
  # X-Fonts are no longer supported by xpdf
  sed -i 's|^displayCIDFontX.*$||' add-to-xpdfrc
  cat >> add-to-xpdfrc << END_OF_RC_TWEAK
displayCIDFontTT	Adobe-CNS1	/usr/share/fonts/TTF/Vera.ttf

END_OF_RC_TWEAK

  install -Dm644 add-to-xpdfrc $pkgdir/etc/xpdf/$pkgname
  install -Dm644 $srcdir/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
