# $Id: PKGBUILD 65230 2010-01-25 13:11:11Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=abs
pkgver=2.3.4.1
pkgrel=1
pkgdesc="Utilities to download and work with the Arch Build System (ABS)"
arch=('i686' 'x86_64')
url="http://projects.archlinux.org/abs.git/"
license=('GPL')
depends=('bash' 'rsync')
backup=(etc/abs.conf)
install=abs.install
source=(ftp://ftp.archlinux.org/other/abs/${pkgname}-${pkgver}.tar.gz)
md5sums=('286825d64f19f801d510ff1f7cc05ea6')

build() {
  cd ${srcdir}/${pkgname}

  make CONFDIR=/etc/ || return 1
  make CONFDIR=/etc/ DESTDIR=${pkgdir} install || return 1

  # Add readme file, and make base /var/abs path
  install -dm0755 ${pkgdir}/var/abs/local/
  install -Dm0644 ${srcdir}/abs/README ${pkgdir}/var/abs/README

  # change ABS tags for x86_64 to correct values
  if [ "$CARCH" = "x86_64" ]; then
    sed -i 's|"i686"|"x86_64"|g' ${pkgdir}/etc/abs.conf
  fi
}
