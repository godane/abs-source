# $Id: $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>
# Contributor: Henning Garus <henning.garus@gmail.com>

pkgname=xdelta3
pkgver=3.0y
pkgrel=1
pkgdesc="A diff utility which works with binary files"
arch=('i686' 'x86_64')
url="http://xdelta.org/"
license=('GPL')
makedepends=('python')
optdepends=('python: for python modules')
source=(http://xdelta.googlecode.com/files/xdelta$pkgver.tar.gz
        xdelta3-makefile.patch)
md5sums=('8246e6ba89a5a6b9efc24f9552fcf940'
         '378e9a735d92fb2fc695ed9ce2fad2d5')

build() {
  cd $srcdir/xdelta$pkgver
  patch -Np1 -i $srcdir/xdelta3-makefile.patch
  
  # xdelta3 binary
  make xdelta3 || return 1
  install -D xdelta3 $pkgdir/usr/bin/xdelta3
  
  # xdelta3main python module
  python ./setup.py install --root ${pkgdir}
  
  # xdelta3 python module
  make xdelta3module.so || return 1
  install -m644 {xdelta3.py,xdelta3module.so} ${pkgdir}/usr/lib/python2.6/site-packages
}

