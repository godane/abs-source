# $Id: PKGBUILD 79266 2010-05-02 14:37:34Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributed by Sarah Hay <sarahhay@mb.sympatico.ca>, Tobias Powalowski <t.powa@gmx.de>, Simo L. <neotuli@yahoo.com>, eric <eric@archlinux.org>

pkgname=sane
pkgver=1.0.21
pkgrel=2
pkgdesc="Scanner Access Now Easy"
url="http://www.sane-project.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libtiff>=3.9.2-2' 'libgphoto2>=2.4.7-2' 'libjpeg>=8' 'libieee1284' 'libusb' 'libv4l' 'avahi' 'bash')
install=$pkgname.install
backup=(etc/sane.d/{abaton.conf,agfafocus.conf,apple.conf,artec.conf,artec_eplus48u.conf,avision.conf,bh.conf,canon.conf,canon630u.conf,canon_dr.conf,canon_pp.conf,cardscan.conf,coolscan2.conf,coolscan3.conf,coolscan.conf,dc25.conf,dc210.conf,dc240.conf,dell1600n_net.conf,dll.conf,dmc.conf,epjitsu.conf,epson.conf,epson2.conf,fujitsu.conf,genesys.conf,gphoto2.conf,gt68xx.conf,hp.conf,hp3900.conf,hp4200.conf,hp5400.conf,hpsj5s.conf,hs2p.conf,ibm.conf,kodak.conf,leo.conf,lexmark.conf,ma1509.conf,matsushita.conf,microtek.conf,microtek2.conf,mustek.conf,mustek_pp.conf,mustek_usb.conf,nec.conf,net.conf,p5.conf,pie.conf,pixma.conf,plustek.conf,plustek_pp.conf,qcam.conf,ricoh.conf,rts8891.conf,s9036.conf,saned.conf,sceptre.conf,sharp.conf,sm3840.conf,snapscan.conf,sp15c.conf,st400.conf,stv680.conf,tamarack.conf,teco1.conf,teco2.conf,teco3.conf,test.conf,u12.conf,umax.conf,umax1220u.conf,umax_pp.conf,xerox_mfp,v4l.conf} etc/xinetd.d/sane)
source=(ftp://ftp.sane-project.org/pub/sane/$pkgname-backends-$pkgver/$pkgname-backends-$pkgver.tar.gz
        ftp://ftp.sane-project.org/pub/sane/$pkgname-backends-$pkgver/sane-backends-1.0.21-i18n.patch
        sane.xinetd)
options=(!libtool)

build() {
  cd "${srcdir}/${pkgname}-backends-${pkgver}"
  patch -Np1 -i ../sane-backends-1.0.21-i18n.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --with-docdir=/usr/share/doc/sane --enable-avahi || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  # fix hp officejets
  echo "#hpaio" >> "${pkgdir}/etc/sane.d/dll.conf" || return 1
  # install udev files
  install -D -m0644 tools/udev/libsane.rules \
    "${pkgdir}/lib/udev/rules.d/53-sane.rules" || return 1
  # fix udev rules
  sed -i 's|NAME="%k", ||g' ${pkgdir}/lib/udev/rules.d/53-sane.rules || return 1
  # fix group on /var/lock
  mkdir -p "${pkgdir}/var/lock/sane"
  chgrp 0 "${pkgdir}/var"
  chmod 755 "${pkgdir}/var"
  chmod 1777 "${pkgdir}/var/lock"
  chgrp 0 "${pkgdir}/var/lock"
  chmod 775 "${pkgdir}/var/lock/sane"
  chgrp 96 "${pkgdir}/var/lock/sane"
  # install xinetd file
  install -D -m644 "${srcdir}/sane.xinetd" "${pkgdir}/etc/xinetd.d/sane"
}
md5sums=('be586a23633949cf2ecf0c9c6d769130'
         '8a6443b731f04dada814e268c42b139a'
         'da946cc36fb83612162cf9505986d4b2')
