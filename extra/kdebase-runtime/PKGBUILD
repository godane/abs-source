# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.4.4
pkgrel=2
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs' 'oxygen-icons' 'xorg-xauth' 'hicolor-icon-theme'
         'smbclient' 'rarian' 'libssh')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'openslp'
             'xine-lib')
optdepends=('htdig: to build the search index'
            'gdb: drkonq crash handler')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('dee7405e7410a7583567a0cc1e76d0d1b0e917ca')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_PulseAudio=OFF || return 1
	make || return 1
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
	rm -f ${pkgdir}/usr/share/icons/hicolor/index.theme
	ln -sf /usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/
}
