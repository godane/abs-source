# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

pkgname=virtuoso
pkgver=6.1.0
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='A scalable cross-platform server that combines SQL/RDF/XML Data Management with Web Application Server and Web Services Platform functionality'
url='http://virtuoso.openlinksw.com/wiki/main/Main/'
license=('GPL')
depends=('openssl')
makedepends=('bison' 'flex')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-opensource-${pkgver}.tar.gz"
        'openssl-1.0.patch')
md5sums=('49135390d27ab3e445f5e9481ab677bb'
         'b4caff9deb6d3f3e4cbbf90967e07360')

build() {
  cd $srcdir/${pkgname}-opensource-${pkgver}
  # see http://cvs.fedoraproject.org/viewvc/rpms/virtuoso-opensource/devel/
  patch -p1 -i ${srcdir}/openssl-1.0.patch || return 1

  ./configure --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-rendezvous \
    --disable-hslookup \
    --disable-all-vads || return 1
  make || return 1
}

package() {
  # install server
  cd ${srcdir}/${pkgname}-opensource-${pkgver}/binsrc/virtuoso
  make DESTDIR=${pkgdir} install || return 1

  # install driver
  cd ${srcdir}/${pkgname}-opensource-${pkgver}/binsrc/driver
  make DESTDIR=${pkgdir} install || return 1
}
