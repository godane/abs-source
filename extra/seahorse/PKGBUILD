# $Id: PKGBUILD 74448 2010-03-30 23:42:44Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=2.30.0
pkgrel=1
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=('GPL')
url="http://seahorse.sourceforge.net/"
depends=('libgnome-keyring>=2.30.0' 'avahi>=0.6.25' 'libnotify>=0.4.5' 'libsoup>=2.30.0' 'gpgme>=1.2.0' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('gettext' 'libldap' 'intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.0' 'openssh' 'libsm')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
replaces=('gnome-keyring-manager')
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/seahorse/2.30/seahorse-${pkgver}.tar.bz2)
sha256sums=('21a17ddac08092b410887e027921d9f522e687adeabe85c85919fc52feec1ab7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-ldap --enable-hkp \
      --enable-libnotify \
      --disable-scrollkeeper --disable-update-mime-database || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain seahorse ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
