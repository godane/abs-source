# $Id: PKGBUILD 69561 2010-02-21 04:47:38Z allan $
# Maintainer: eric <eric@archlinux.org>
# Contributor : Markus Meissner <markus@meissna.de>

pkgname=tidyhtml
# obtain pkgver from http://tidy.cvs.sourceforge.net/tidy/tidy/src/version.h?view=markup
pkgver=1.46
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=('i686' 'x86_64')
url="http://tidy.sourceforge.net/"
license=('custom')
options=('!libtool')
source=(ftp://ftp.archlinux.org/other/tidyhtml/${pkgname}-${pkgver}.tar.bz2)
md5sums=('3d970d54be83c5ec985687e9976a2832')

mksource() {
  mkdir tidyhtml-${pkgver}
  cd tidyhtml-${pkgver}
  cvs -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy login   #hit enter for password
  cvs -z3 -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy co -P tidy
  cd ..
  tar -cvjf tidyhtml-${pkgver}.tar.bz2 tidyhtml-${pkgver}/*
}

build(){
  cd $srcdir/${pkgname}-${pkgver}/tidy
  source build/gnuauto/setup.sh
  
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=$pkgdir install
  
  install -Dm644 htmldoc/license.html \
    ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
