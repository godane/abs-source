# $Id: PKGBUILD 75454 2010-04-01 16:24:19Z giovanni $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Mildred <mildred593 at online dot fr>

pkgname=claws-mail-extra-plugins
pkgver=3.7.5
pkgrel=2
pkgdesc="Extra plugins for claws-mail"
url="http://www.claws-mail.org/plugins.php?branch=EXT"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('claws-mail>=3.7.5')
makedepends=('make' 'bc' 'perl>=5.10.1-5' 'libgtkhtml>=2.11.1-2' 'libnotify>=0.4.5' 'libxml2>=2.7.6' 'curl>=7.19.7' 'libytnef>=1.5-2' 'libwebkit>=1.1.15.4-2' 'libchamplain>=0.4.4' 'pygtk>=2.16.0')
optdepends=('libgtkhtml: for gtkhtml2_viewer plugin'
            'perl: for perl plugin'
            'libnotify: for notification plugin'
            'libxml2: for gtkhtml2_viewer and rssyl plugins'
            'curl: for gtkhtml2_viewer, vcalendar, rssyl and spamreport plugins'
            'libarchive: for archive plugin'
            'libytnef: for tnef_parse plugin'
            'libwebkit: for the fancy webkit html plugin'
            'libchamplain: for geolocation plugin')
conflicts=('claws-gtkhtml2_viewer' 'claws-mail-acpinotifier-plugin' 'sylpheed-claws-gtkhtml2-plugin' 'sylpheed-claws-rssyl-plugin' 'sylpheed-claws-extra-plugins' 'claws-tnef-plugin' 'claws-webkit-plugin-svn' 'claws-mayflower-plugin-svn')
replaces=('sylpheed-claws-extra-plugins')
options=('!libtool')
source=(http://downloads.sourceforge.net/project/sylpheed-claws/extra%20plugins/$pkgver/claws-mail-extra-plugins-$pkgver.tar.bz2)
md5sums=('ff3fe3e242edcb8cb9a98d98285121e3')

build() {
    cd "${srcdir}/claws-mail-extra-plugins-$pkgver"
    for dir in *; do
        # Continue if it is not a directory or the synce plugin
        [ ! -d "$dir" ] && continue
        echo "$dir" | grep synce >/dev/null 2>/dev/null && continue
         # Else, compile
        cd "$dir"
        [ -e "$configured_file" ] || \
            ./configure --prefix=/usr --disable-static || return 1
        touch "arch-configured"
        make || return 1
        make DESTDIR=$startdir/pkg install || return 1
        cd ..
    done

    # fix fileconflict with libical, #11485
    rm -f ${pkgdir}/usr/include/ical.h
}
