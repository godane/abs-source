# $Id: PKGBUILD 73332 2010-03-25 10:38:10Z daniel $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Brice Carpentier <brice@dlfp.org>

pkgname=mono
pkgver=2.6.3
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=(i686 x86_64)
license=('GPL' 'LGPL2' 'MPL' 'custom:MITX11')
url="http://www.mono-project.com/"
depends=('zlib' 'libgdiplus>=2.6.2' 'sh')
makedepends=('pkgconfig')
options=('!libtool' '!makeflags')
provides=('monodoc')
conflicts=('monodoc')
source=(http://ftp.novell.com/pub/mono/sources/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        mono.rc.d)
md5sums=('b1dc21bac2c7c75814a9f32246eadadd'
         '8315e46c6a6e9625502521fc0ad1a322')

build() {
  mkdir -p ${startdir}/pkg/usr/share/licenses/$pkgname
  # build mono
  cd ${startdir}/src/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc \
              --with-libgdiplus=installed \
	      --with-moonlight=yes \
	      --with-profile4=yes
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  # install daemons and pathes
  mkdir -p ${startdir}/pkg/etc/rc.d
  install -m755 ${startdir}/src/mono.rc.d $startdir/pkg/etc/rc.d/mono

  #install license
  install -m644 mcs/MIT.X11 ${startdir}/pkg/usr/share/licenses/$pkgname/
}

