# $Id: PKGBUILD 81950 2010-06-07 12:08:34Z jgc $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=gnumeric
pkgver=1.10.5
pkgrel=1
pkgdesc="A GNOME Spreadsheet Program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice>=0.8.5' 'gconf>=2.28.1' 'desktop-file-utils' 'libglade>=2.6.4')
makedepends=('intltool' 'python' 'gnome-doc-utils>=0.20.0' 'pygobject' 'psiconv')
optdepends=('pygobject: for python plugin support'
            'psiconv: for Psion 5 file support'
            'yelp: for viewing the help manual')
install=gnumeric.install
options=('libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('2b32c0f1f00870149de85f4dc86d316d2287db4b4950a6d7afa4155287e7476a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-install --enable-ssindex || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnumeric "${pkgdir}"/etc/gconf/schemas/*.schemas || return 1
  rm -rf "${pkgdir}/etc/gconf"
}
