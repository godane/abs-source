# $Id: PKGBUILD 76005 2010-04-05 19:56:43Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.2.0
pkgrel=1
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'gtk2' 'icu>=4.4' 'gstreamer0.10-base' 'libsoup' 'enchant')
makedepends=('gperf' 'gtk-doc' 'gir-repository')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
options=('!libtool')
source=("http://webkitgtk.org/webkit-${pkgver}.tar.gz"
        'icu-4.4.patch')
md5sums=('b2bfcbc4bbf1d0a51f848cb54c21ee66'
         'ea289da2a03857e946cb835fd9e95097')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  patch -p2 -i $srcdir/icu-4.4.patch || return 1
  ./configure --prefix=/usr \
      --enable-video --enable-introspection \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu || return 1
  make || return 1
}

package() {
  cd "${srcdir}/webkit-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
