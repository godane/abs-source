# $Id: PKGBUILD,v 1.31 2009/03/14 02:03:57 allan Exp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=2.30.1
pkgrel=1
pkgdesc="A disc burning application for Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!libtool' '!emptydirs')
depends=('gstreamer0.10-base-plugins>=0.10.26' 'totem-plparser>=2.30.0' 'libbeagle>=0.3.9' 'cdrkit' 'cdrdao' 'desktop-file-utils' 'hicolor-icon-theme' 'libunique>=1.1.6' 'gconf>=2.28.1' 'libcanberra' 'dvd+rw-tools')
makedepends=('gnome-doc-utils>=0.20.1' 'intltool' 'libburn' 'libisofs' 'nautilus' 'gobject-introspection')
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'gstreamer0.10-bad-plugins: video project'
            'vcdimager: video project')
replaces=('nautilus-cd-burner')
install=brasero.install
source=(http://ftp.gnome.org/pub/gnome/sources/brasero/2.30/brasero-${pkgver}.tar.bz2)
sha256sums=('676922e42192f38c82b40b460f896fa4653dd55a336365ace1fa64d9fc441f0e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-caches || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1

  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain brasero ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
