# $Id: PKGBUILD 80286 2010-05-14 23:29:59Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=fvwm-devel
pkgver=2.5.30
pkgrel=1
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('i686' 'x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('imlib' 'fribidi' 'perl' 'libstroke' 'libxpm' 'readline' 'librsvg')
makedepends=('pkgconfig')
conflicts=('fvwm')
provides=("fvwm=${pkgver}")
source=(ftp://ftp.fvwm.org/pub/fvwm/version-2/fvwm-${pkgver}.tar.bz2 fvwm-devel.desktop)
md5sums=('9d1f787e5ad5a8f886a8e711ec2e5690' '3f5bcb246e035ff29aa5bf54487a3db4')
sha1sums=('fcc082c8e13b262bb4f86f24ed8142a77c10b4f3' '17db773870637b410ceb4f68743dbd517bc5c599')

build() {
  cd "${srcdir}/fvwm-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke-library || return 1
  make || return 1
}

package() {
  cd "${srcdir}/fvwm-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1

  install -d "${pkgdir}/etc/fvwm"
  install -D -m644 sample.fvwmrc/* "${pkgdir}/etc/fvwm" || return 1
  rm -f "${pkgdir}"/etc/fvwm/Makefile*
  install -D -m644 ../fvwm-devel.desktop "${pkgdir}/etc/X11/sessions/fvwm-devel.desktop" || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
}
