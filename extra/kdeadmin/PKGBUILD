# $Id: PKGBUILD 73491 2010-03-26 19:19:16Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeadmin
pkgname=('kdeadmin-kcron'
         'kdeadmin-ksystemlog'
         'kdeadmin-kuser'
         'kdeadmin-system-config-printer-kde')
pkgver=4.4.2
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'kdebase-runtime'
             'kdebindings-python' 'system-config-printer-common' 'kdepim-runtime')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
	'fix-syslog-path.patch')
md5sums=('92d80ac84525c8160f42bf54ee6351df'
         '41b71823c7a33975ec08aaf75d67a374')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	patch -p1 -i ${srcdir}/fix-syslog-path.patch || return 1

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_kpackage=OFF \
		-DBUILD_knetworkconf=OFF \
		-DBUILD_lilo-config=OFF \
		-DBUILD_strigi-analyzer=OFF
	make
}

package_kdeadmin-kcron() {
	pkgdesc='Configure and schedule tasks'
	depends=('kdelibs')
	cd $srcdir/build/kcron
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcron
	make DESTDIR=$pkgdir install
}

package_kdeadmin-ksystemlog() {
	pkgdesc='System log viewer tool'
	depends=('kdelibs' 'kdebase-runtime')
	cd $srcdir/build/ksystemlog
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ksystemlog
	make DESTDIR=$pkgdir install
}

package_kdeadmin-kuser() {
	pkgdesc='User Manager'
	depends=('kdepimlibs' 'kdebase-runtime' 'kdepim-runtime')
	install=kdeadmin.install
	cd $srcdir/build/kuser
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kuser
	make DESTDIR=$pkgdir install
}

package_kdeadmin-system-config-printer-kde() {
	pkgdesc='Configure local and remote Printers'
	depends=('kdebindings-python' 'system-config-printer-common')
	cd $srcdir/build/system-config-printer-kde
	make DESTDIR=$pkgdir install
}
