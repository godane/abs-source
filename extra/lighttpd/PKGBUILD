# $Id: PKGBUILD 75266 2010-04-01 06:25:32Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=lighttpd
pkgver=1.4.26
pkgrel=3
pkgdesc='a secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('i686' 'x86_64')
url="http://www.lighttpd.net/"
depends=('pcre' 'openssl' 'zlib' 'bzip2' 'attr' 'libldap' 'util-linux-ng')
makedepends=('fcgi' 'libmysqlclient' 'lua' 'libxml2' 'e2fsprogs' 'sqlite3' 'gdbm' 'pkgconfig')
optdepends=('libxml2: mod_webdav'
            'lua: mod_cml/mod_magnet'
            'libmysqlclient: mod_mysql_vhost'
            'sqlite3: mod_webdav')
backup=('etc/lighttpd/lighttpd.conf' 'etc/logrotate.d/lighttpd')
options=('!libtool' 'emptydirs')
source=("http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.bz2"
        'lighttpd.rc.d' 'lighttpd.logrotate.d' 'openssl-0.9.8m.patch')
md5sums=('a682c8efce47a2f4263a247ba0813c9b'
         '789ed1b4521e72e591e09d5dfb99235a'
         '857e174643fd7761a2f0d8431a679f6c'
         '37b7ca9883d5accaae84dcd7329576ae')

build() {
	cd $srcdir/$pkgname-$pkgver

	# fixed in 1.4.27
	patch -p0 -i ${srcdir}/openssl-0.9.8m.patch || return 1

	./configure --prefix=/usr \
		--libexecdir=/usr/lib/lighttpd/modules \
		--sysconfdir=/etc/lighttpd \
		--sharedstatedir=/usr/var \
		--localstatedir=/var \
		--libdir=/usr/lib/lighttpd \
		--includedir=/usr/include/lighttpd \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-webdav-props \
		--with-webdav-locks \
		--with-gdbm \
		--with-memcache \
		--with-lua
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -D -m755 $srcdir/lighttpd.rc.d $pkgdir/etc/rc.d/lighttpd
	install -D -m644 $srcdir/lighttpd.logrotate.d $pkgdir/etc/logrotate.d/lighttpd
	install -d -m755 -o http -g http $pkgdir/var/run/lighttpd/
	install -d -m755 -o http -g http $pkgdir/var/log/lighttpd/

	install -D -m644 doc/lighttpd.conf $pkgdir/etc/lighttpd/lighttpd.conf

	# set sane defaults
	sed -e 's|/srv/www/htdocs/|/srv/http/|' \
	    -e 's|/srv/www/|/srv/http/|' \
	    -e 's|#server.username            = "wwwrun"|server.username            = "http"|' \
	    -e 's|#server.groupname           = "wwwrun"|server.groupname           = "http"|' \
	    -e 's|#server.pid-file            = "/var/run/lighttpd.pid"|server.pid-file            = "/var/run/lighttpd/lighttpd.pid"|' \
	    -e 's|/usr/local/bin/php-cgi|/usr/bin/php-cgi|' \
	    -i ${pkgdir}/etc/lighttpd/lighttpd.conf

	install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
