# $Id: PKGBUILD 77001 2010-04-09 14:26:11Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=empathy
pkgver=2.30.0.1
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('iso-codes' 'telepathy-mission-control>=5.3.1' 'telepathy-farsight>=0.0.12' 'libcanberra' 'libnotify' 'libunique>=1.1.6' 'libwebkit>=1.2.0' 'evolution-data-server>=2.30')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils>=0.19.5' 'nautilus-sendto')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('8c7722058aab2f8d8b8a6d527c219582cb28957b32d9f001f520a107ffbdade1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-schemas-install || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
      DESTDIR="${pkgdir}" install || return 1

  # Merge schemas file in a single file and remove trash
  mkdir -p "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
      --domain empathy "${pkgdir}/etc/gconf/schemas/"*.schemas
  rm -rf "${pkgdir}/etc/gconf"
}
