# $Id: PKGBUILD 24494 2009-01-17 20:42:01Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libproxy
pkgver=0.3.1
pkgrel=1
pkgdesc="A library that provides automatic proxy configuration management"
arch=(i686 x86_64)
license=('LGPL')
depends=('glibc')
makedepends=('libxmu' 'gconf' 'networkmanager')
optdepends=('libxmu: KDE and GNOME plugins'
            'gconf: GNOME plugin'
	    'networkmanager: NetworkManager plugin')
url="http://libproxy.googlecode.com"
source=(http://libproxy.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
	nm-dbus.patch)
options=('!libtool')
md5sums=('1745fe1bdf5beca1bf7269758f3906e8'
         'a56ee1519bc3640257430b5fe5540a38')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/nm-dbus.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --prefix=/usr --without-mozjs || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
