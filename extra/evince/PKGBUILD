# $Id: PKGBUILD 78993 2010-04-29 19:08:36Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=2.30.1
pkgrel=2
pkgdesc="Simply a document viewer"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libspectre>=0.2.3' 'gsfonts' 'poppler-glib>=0.12.4' 'libdjvu>=3.5.22' 'gnome-icon-theme>=2.30.2' 't1lib' 'libgnome-keyring>=2.30.1' 'desktop-file-utils')
makedepends=('gnome-doc-utils>=0.20.1' 'nautilus>=2.30.1' 'pkgconfig' 'texlive-bin' 'intltool' 'gobject-introspection')
optdepends=('texlive-bin: DVI support')
replaces=('gpdf')
groups=('gnome-extra')
install=evince.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
       fix_creating_last_settings.patch)
sha256sums=('ab6b957ab422341f023baa8ee4061bc5719389f92d6745874d239cd1e737f225'
 	    '8a6b6fc4752ead2f2843782e581ae66717cea62a778a39f17da5a9ca82014f9d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/fix_creating_last_settings.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/evince \
      --disable-static --enable-nautilus \
      --enable-pdf --enable-tiff \
      --enable-djvu --enable-dvi \
      --enable-t1lib --enable-pixbuf \
      --enable-comics --enable-impress \
      --enable-introspection \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain evince ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
