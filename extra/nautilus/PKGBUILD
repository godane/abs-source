# $Id: PKGBUILD 73909 2010-03-29 10:32:20Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgver=2.30.0
pkgrel=1
pkgdesc="The GNOME shell and file manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif>=0.6.19' 'exempi>=2.1.1' 'gnome-icon-theme>=2.29.3' 'desktop-file-utils' 'gvfs>=1.6.0' 'libunique>=1.1.6' 'gnome-desktop>=2.29.92')
makedepends=('pkgconfig' 'intltool')
replaces=('gnome-volume-manager')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=nautilus.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        nautilus-condrestart.patch)
sha256sums=('01ecbc4b8b9a0ac726194eb81d3de7080ee52d885eb1898692aa951b093731a5'
            'f825292ef26274d3044272ca9f25095391cc49d30fb0056e7ed7346a1b4d106c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/nautilus-condrestart.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-update-mimedb || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain nautilus ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
