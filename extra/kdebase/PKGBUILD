# $Id: PKGBUILD 59827 2009-11-28 01:40:24Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebase
pkgname=('kdebase-dolphin'
         'kdebase-kappfinder'
         'kdebase-kdepasswd'
         'kdebase-kdialog'
         'kdebase-kfind'
         'kdebase-kinfocenter'
         'kdebase-konqueror'
         'kdebase-konsole'
         'kdebase-kwrite'
         'kdebase-lib'
         'kdebase-plasma')
pkgver=4.4.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
makedepends=('kdebase-workspace' 'pkgconfig' 'cmake' 'automoc4' 'libraw1394' 'ruby')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('96c8b34fa388831fba2fe154b27d04514e87b34f')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdebase-dolphin() {
	pkgdesc='File Manager'
	depends=('kdebase-runtime' 'kdebase-lib')
	optdepends=('ruby: servicemenu installation')
	cd $srcdir/build/apps/dolphin
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/dolphin
	make DESTDIR=$pkgdir install
}

package_kdebase-kappfinder() {
	pkgdesc='Menu Updating Tool'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/kappfinder
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kappfinder
	make DESTDIR=$pkgdir install
	# FIXME (oxygen-icons-svg)
	rm ${pkgdir}/usr/share/icons/oxygen/scalable/apps/clock.svgz
}

package_kdebase-kdepasswd() {
	pkgdesc='Change Password'
	depends=('kdebase-runtime' 'kdebase-lib')
	cd $srcdir/build/apps/kdepasswd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kdepasswd
	make DESTDIR=$pkgdir install
}

package_kdebase-kdialog() {
	pkgdesc='a utility for displaying dialog boxes from shell scripts'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/apps/kdialog
	make DESTDIR=$pkgdir install
}

package_kdebase-kfind() {
	pkgdesc='Find Files/Folders'
	depends=('kdebase-runtime' 'kdebase-lib')
	install='kdebase.install'
	cd $srcdir/build/apps/kfind
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kfind
	make DESTDIR=$pkgdir install
}

package_kdebase-kinfocenter() {
	pkgdesc='Info Center'
	depends=('kdebase-runtime' 'libraw1394' 'kdelibs')
	cd $srcdir/build/apps/kinfocenter
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kinfocenter
	make DESTDIR=$pkgdir install
}

package_kdebase-konqueror() {
	pkgdesc='KDE File Manager & Web Browser'
	depends=('kdebase-runtime' 'kdebase-lib' 'kdebase-dolphin')
	install='kdebase.install'
	conflicts=('kdebase-keditbookmarks' 'kdebase-nsplugins')
	replaces=('kdebase-keditbookmarks' 'kdebase-nsplugins')
	for i in konqueror doc/konqueror keditbookmarks nsplugins; do
		cd $srcdir/build/apps/${i}
		make DESTDIR=$pkgdir install
	done
}

package_kdebase-konsole() {
	pkgdesc='Terminal'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/apps/konsole
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/konsole
	make DESTDIR=$pkgdir install
}

package_kdebase-kwrite() {
	pkgdesc='Text Editor'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/apps/kwrite
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kwrite
	make DESTDIR=$pkgdir install
}

package_kdebase-lib() {
	pkgdesc='KDE libraries for the basic desktop applications'
	groups=()
	depends=('kdelibs')
	cd $srcdir/build/apps/lib
	make DESTDIR=$pkgdir install
}

package_kdebase-plasma() {
	pkgdesc='Display the contents of folders (UserÂ´s home folder as default)'
	depends=('kdebase-workspace' 'kdebase-lib')
	cd $srcdir/build/apps/plasma
	make DESTDIR=$pkgdir install
}
