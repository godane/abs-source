# $Id: PKGBUILD 75539 2010-04-01 19:12:18Z andrea $
# Maintainer:
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=bigloo
pkgver=3.3a_5
_pkgver=3.3a-5
pkgrel=2
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
depends=('openssl' 'sqlite3' 'texinfo')
makedepends=('jdk')
options=('!makeflags')
install=bigloo.install
source=(ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${pkgname}${_pkgver}.tar.gz)
md5sums=('cc07aefa51e14f3f8dbdaca9bc77386b')

build() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --dotnet=no \
    --javaprefix=/opt/java/bin || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  make -C manuals DESTDIR="${pkgdir}" install-man-bee || return 1
  chmod 644 "${pkgdir}"/usr/lib/bigloo/3.3a/*.a || return 1
}
