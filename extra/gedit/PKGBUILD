# $Id: PKGBUILD 74079 2010-03-30 13:31:48Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=2.30.0
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gconf>=2.28.0' 'pygtksourceview2>=2.10.0' 'enchant>=1.5.0' 'desktop-file-utils' 'iso-codes>=3.10.1' 'libsm')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.19.5' 'intltool')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('37598473372aab217e46f19726cff616ff0ea4121bbdbb170b4e264a4ca76690')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gedit ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
