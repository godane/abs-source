# $Id: PKGBUILD 69602 2010-02-21 13:25:09Z daniel $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Rashif "Don Ray" Rahman <rayrashif@gmail.com>

pkgname=wicd
pkgver=1.7.0
pkgrel=3
pkgdesc="New and alternative wireless/wired network management utility"
arch=('i686' 'x86_64')
url="http://wicd.sourceforge.net/"
license=('GPL2')
depends=('python' 'dbus-python' 'pygtk' 'dhcpcd' 'wpa_supplicant' 'wireless_tools' 'ethtool' 'shared-mime-info' 'python-urwid')
optdepends=('hicolor-icon-theme'
            'gksu:		needed to access some preferences in gtk interface'
            'notification-daemon: needed if you want notifications'
            'python-notify:	needed if you want notifications'
            'python-wpactrl:	needed if you want to use the new experimental ioctrl backend'
	    'python-iwscan:	needed if you want to use the new experimental ioctrl backend')
conflicts=('wicd-svn')
install=wicd.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz
        wicd-daemon
        wicd.desktop
	wicd-scripts-execution.patch)
options=('emptydirs')
backup=('etc/wicd/encryption/templates/active')
md5sums=('003d2e67240989db55934553437ba32a'
         'f40e5f59998d0829707a7c9976afa8f8'
         '326df163a5732d38741371baa4fce9e5'
         'f4c377a25aa077cb76955124adfcc03f')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p0 <$srcdir/wicd-scripts-execution.patch || return 1
  python setup.py configure --no-install-init --resume=/usr/share/wicd/scripts/ \
                            --suspend=/usr/share/wicd/scripts/ --verbose
  python setup.py install --root=$pkgdir

  # Add custom rc.d script
  install -Dm755 $srcdir/wicd-daemon $pkgdir/etc/rc.d/wicd
  install -Dm644 $srcdir/wicd.desktop $pkgdir/usr/share/applications/wicd.desktop

  cd build/lib/wicd
  for i in *.py; do
    install -Dm 755 $i $startdir/pkg/usr/lib/wicd/$i
  done
  
  rm -rf  ${startdir}/pkg/usr/share/autostart
}

