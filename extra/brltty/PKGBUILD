# $Id: PKGBUILD 75726 2010-04-02 20:08:43Z pierre $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty
pkgver=4.1
pkgrel=3
pkgdesc="Braille display driver for Linux/Unix"
arch=('i686' 'x86_64')
url="http://mielke.cc/brltty"
license=('GPL' 'LGPL')
depends=('libxaw' 'at-spi' 'gpm' 'icu' 'python' 'tcl' 'atk' 'libxtst')
makedepends=('pyrex' 'bluez')
optdepends=('bluez: bluetooth support')
backup=(etc/brltty.conf etc/conf.d/brltty.conf)
options=('!makeflags' '!emptydirs')
source=(http://mielke.cc/${pkgname}/releases/${pkgname}-${pkgver}.tar.gz
	'brltty' 'brltty.conf')
md5sums=('b3b492240ba33fd3af45a181f5b89e7a'
         '831ebaf0c56091702929c68805d20c4f'
         'a8ab8b3dd059e96e1734bc9cdcf844fc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+="${CFLAGS} -D_GNU_SOURCE" \
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --localstatedir=/var \
              --enable-gpm \
              --disable-java-bindings \
	      --disable-caml-bindings
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
  install -D -m 755 ${srcdir}/brltty ${pkgdir}/etc/rc.d/brltty
  install -D -m 644 ${srcdir}/brltty.conf ${pkgdir}/etc/conf.d/brltty.conf
  install -D -m 644 Documents/brltty.conf ${pkgdir}/etc/brltty.conf
}
