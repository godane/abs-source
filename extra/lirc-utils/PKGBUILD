# $Id: PKGBUILD 56261 2009-10-20 19:13:10Z hugo $
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=lirc-utils
pkgver=0.8.6
pkgrel=3
pkgdesc="Linux Infrared Remote Control utils"
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
_kernver=2.6.31-ARCH
depends=('alsa-lib' 'libusb' 'libx11' 'libsm' 'python' 'libftdi')
makedepends=('help2man')
replaces=('lirc+pctv')
backup=('etc/conf.d/lircd.conf' 'etc/conf.d/lircmd.conf' 'etc/lirc/lircd.conf')
options=('!libtool' '!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/lirc/lirc-$pkgver.tar.bz2
	lircd lircmd lirc.logrotate lircd.conf irexec.conf irexecd)

build() {
	# configure
	cd ${srcdir}/lirc-$pkgver || return 1

      # Disabling lirc_gpio driver as it does no longer work Kernel 2.6.22+
	sed -i -e "s:lirc_gpio\.o::" drivers/lirc_gpio/Makefile.am || return 1

	autoreconf || return 1
	libtoolize || return 1

	./configure --enable-sandboxed --prefix=/usr \
	--with-driver=all --with-kerneldir=/usr/src/linux-${_kernver} \
	--with-moduledir=/lib/modules/${_kernver}/kernel/drivers/misc \
	--with-transmitter \
		|| return 1
	# disable parallel and bt829
        # because of incompatibility with smp systems
        sed -i -e "s:lirc_parallel::" -e "s:lirc_bt829::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile \
                || return 1

  	# build
	make || return 1
	make DESTDIR=${pkgdir} install || return 1
	mkdir -p ${pkgdir}/usr/share/lirc ${pkgdir}/etc/rc.d \
		|| return 1
	cp ${srcdir}/{lircd,lircmd,irexecd} ${pkgdir}/etc/rc.d/ \
		|| return 1
	cp -rp remotes ${pkgdir}/usr/share/lirc || return 1
	chmod -R go-w ${pkgdir}/usr/share/lirc/ || return 1


	# install the logrotate config
    	install -D -m644 ${srcdir}/lirc.logrotate \
        	${pkgdir}/etc/logrotate.d/lirc || return 1
    
	# install conf.d file
	install -D -m644 ${srcdir}/lircd.conf \
        	${pkgdir}/etc/conf.d/lircd.conf || return 1

	# install conf.d file
        install -D -m644 ${srcdir}/irexec.conf \
                ${pkgdir}/etc/conf.d/irexec.conf || return 1

	install -d -m755 ${pkgdir}/etc/lirc || return 1
	install -d -m755 ${pkgdir}/var/run/lirc || return 1

	# remove built modules
	rm -r ${pkgdir}/lib/
}
md5sums=('4ca24da6f5e7c2dcea74878c27a4a3f7'
         '310f718169b16b6afa6615fa53a0ef21'
         '85f7fdac55e5256967241864049bf5e9'
         '3deb02604b37811d41816e9b4385fcc3'
         '5b1f8c9cd788a39a6283f93302ce5c6e'
         'f0c0ac930326168035f0c8e24357ae55'
         '618ca4f666341d6ade8c616ce59f4d1b')
