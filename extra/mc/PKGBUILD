# $Id: PKGBUILD 79804 2010-05-07 22:48:52Z dgriffiths $
# Contributer: dorphell <dorphell@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=mc
pkgver=4.7.2
pkgrel=1
pkgdesc="A filemanager/shell that emulates Norton Commander"
arch=('i686' 'x86_64')
url="http://www.ibiblio.org/mc/"
license=('GPL')
depends=('e2fsprogs' 'glib2' 'pcre' 'gpm>=1.20.4' 'slang')
makedepends=('libxt' 'libx11')
optdepends=('p7zip: support for 7zip archives')
provides=('mcedit-pkgbuild-syntax')
conflicts=('mc-utf8')
replaces=('mc-utf8')
options=('!emptydirs' '!makeflags')
source=(http://www.midnight-commander.org/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('82d0f923f686e462eeb739760f8c45bd')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure \
	    --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
	    --enable-background --enable-charset --enable-largefile \
	    --with-edit --with-gpm-mouse --with-mmap --with-samba \
	    --with-screen=slang --with-subshell --with-vfs --with-x \
	    --without-debug --without-gnome --without-included-gettext \
	    --libexecdir=/usr/lib

	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	# Fix FS#15177
	sed -i 's|op_has_zipinfo = 0|op_has_zipinfo = 1|' \
	    ${pkgdir}/usr/lib/mc/extfs.d/uzip

	# Fix FS#18312
	rm ${pkgdir}/usr/lib/mc/extfs.d/u7z
}
