# $Id: PKGBUILD 73462 2010-03-26 09:49:26Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

pkgname=bluez
pkgver=4.63
pkgrel=1
pkgdesc="Libraries and tools for the Bluetooth protocol stack"
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dbus-core')
makedepends=('gstreamer0.10-base' 'alsa-lib' 'libusb' 'libnl')
optdepends=('gstreamer0.10-base' 'alsa-lib' 'libusb' 'libnl' 'dbus-python')
conflicts=('bluez-libs' 'bluez-utils')
provides=('bluez-libs' 'bluez-utils')
replaces=('bluez-libs' 'bluez-utils')
options=('!libtool')
backup=(etc/bluetooth/{main,rfcomm,audio,network,input}.conf
        etc/conf.d/bluetooth)
source=(http://www.kernel.org/pub/linux/bluetooth/${pkgname}-${pkgver}.tar.bz2
        bluetooth.conf.d
        rc.bluetooth)
md5sums=('bb388f3dbb69cd253f23fc32b0f49ffa'
         'd12be5b494525bb1ba6eac5c3983dd3e'
         'b67bd953082befea6ae57c6fe25993e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/lib \
        --mandir=/usr/share/man \
        --enable-gstreamer \
        --enable-alsa \
        --enable-usb \
        --enable-netlink \
        --enable-tools \
        --enable-bccmd \
        --enable-hid2hci \
        --enable-dfutool \
        --enable-hidd \
        --enable-pand \
        --enable-dund \
        --enable-cups \
        --enable-configfiles || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  install -Dm755 ${srcdir}/rc.bluetooth ${pkgdir}/etc/rc.d/bluetooth || return 1
  
  install -d ${pkgdir}/etc/bluetooth
  install -m644 network/network.conf input/input.conf audio/audio.conf \
    ${pkgdir}/etc/bluetooth/ || return 1
  
  install -Dm644 ${srcdir}/bluetooth.conf.d \
    ${pkgdir}/etc/conf.d/bluetooth || return 1

  install -Dm755 scripts/bluetooth_serial \
    ${pkgdir}/lib/udev/bluetooth_serial || return 1

  install -Dm755 test/simple-agent \
    ${pkgdir}/usr/bin/bluez-simple-agent || return 1
}
