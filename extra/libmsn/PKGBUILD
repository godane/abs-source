# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Charly Ghislain <charlyghislain@gmail.com>

pkgname=libmsn
pkgver=4.1
pkgrel=2
pkgdesc="A reusable, open-source, fully documented library for connecting to Microsoft's MSN"
url="http://sourceforge.net/projects/libmsn/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('openssl')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
	'openssl1.patch')
md5sums=('769d3cf0e5625263fbc23321c1ddfcb0'
	'c8d7e2113d967a1d38a531e9fe8e7861')

build() {
  cd ${srcdir}
  patch -Np0 -i ${srcdir}/openssl1.patch || return 1
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release || return 1
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
