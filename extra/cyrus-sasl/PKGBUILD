# $Id: PKGBUILD 75335 2010-04-01 10:43:20Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cyrus-sasl
pkgver=2.1.23
pkgrel=4
pkgdesc="SASL authentication daemon"
arch=('i686' 'x86_64')
license=('custom')
url="http://asg.web.cmu.edu/cyrus/download/"
depends=('pam>=1.0.1-2' 'heimdal>=1.3.1' 'libldap' 'cyrus-sasl-plugins>=2.1.22-7' 'db>=4.7')
replaces=(cyrus-sasl-mysql cyrus-sasl-pgsql)
conflicts=(cyrus-sasl-mysql cyrus-sasl-pgsql)
backup=(etc/conf.d/saslauthd)
source=(ftp://ftp.andrew.cmu.edu/pub/cyrus-mail/${pkgname}-${pkgver}.tar.gz
	saslauthd
	saslauthd.conf.d) 
md5sums=('2eb0e48106f0e9cd8001e654f267ecbc'
         '697dfb51206c398bc976ce9f4cffe72d'
         '96d8a2f6189501f8044838e04d5cae7f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-ldap=/usr --with-saslauthd=/var/run/saslauthd
  cd saslauthd
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  make testsaslauthd || return 1
  install -m755 testsaslauthd "${pkgdir}/usr/sbin" || return 1

  install -dm766 "${pkgdir}/var/run/saslauthd"
  install -Dm755 "${srcdir}/saslauthd" "${pkgdir}/etc/rc.d/saslauthd"
  install -Dm644 "${srcdir}/saslauthd.conf.d" "${pkgdir}/etc/conf.d/saslauthd"

  install -Dm644 ../COPYING "${pkgdir}/usr/share/licenses/cyrus-sasl/COPYING"
}
