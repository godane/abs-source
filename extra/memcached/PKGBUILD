# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.4.5
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=(i686 x86_64)
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
backup=('etc/conf.d/memcached')
source=(http://memcached.googlecode.com/files/$pkgname-$pkgver.tar.gz
        memcached.conf
        memcached.sh)

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install || return 1
  install -D -m 755 scripts/memcached-tool $pkgdir/usr/bin/memcached-tool
  install -D -m 644 $srcdir/$pkgname.conf $pkgdir/etc/conf.d/$pkgname
  install -D -m 755 $srcdir/$pkgname.sh $pkgdir/etc/rc.d/$pkgname
}

md5sums=('583441a25f937360624024f2881e5ea8'
         'bf15619930dadf0c08669566e4aa809c'
         '65f88b69cf3112b178725af121327765')
