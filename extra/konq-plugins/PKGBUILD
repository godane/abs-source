# $Id: PKGBUILD 41669 2009-06-06 10:21:13Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=konq-plugins
pkgver=4.4.0
pkgrel=1
pkgdesc="Konqueror Plugins"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
groups=('kde-extragear')
depends=('kdebase-konqueror' 'tidyhtml')
makedepends=('pkgconfig' 'cmake' 'automoc4')
options=('docs')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/extragear/${pkgname}-${pkgver}.tar.bz2")
md5sums=('7eb163f429adf035c1495d1cbb05d4d2')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
