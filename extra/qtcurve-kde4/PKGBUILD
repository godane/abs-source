# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qtcurve-kde4
pkgver=1.4.3
pkgrel=1
arch=('i686' 'x86_64')
groups=('qtcurve')
license=('GPL')
pkgdesc='A configurable set of widget styles for KDE and Gtk'
url='http://www.kde-look.org/content/show.php?content=40492'
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
source=("http://craigd.wikispaces.com/file/view/QtCurve-KDE4-${pkgver}.tar.bz2")
md5sums=('bfa9e49350ba46e77c70bb8a93b036dc')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../QtCurve-KDE4-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make || return 1
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install || return 1
}
