# $Id: PKGBUILD 62098 2010-01-02 09:13:44Z tpowa $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=0.6.1
_pkgver=61
pkgrel=1
pkgdesc="An instrument neutral distributed interface control protocol that aims to provide backend driver support and automation for a wide range of Astronomical devices (telescopes, focusers, CCDs..etc)"
url="http://www.indilib.org"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'zlib' 'libnova' 'cfitsio')
makedepends=('pkgconfig' 'cmake')
provides=('indilib')
replaces=('indilib')
conflicts=('indilib')
options=(!libtool)
source=("http://downloads.sourceforge.net/indi/${pkgname}0_${_pkgver}.tar.gz")
md5sums=('005e6ae3bc6fc1eeb5aa7700a65e2584')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr || return 1
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
