# $Id: PKGBUILD 75159 2010-03-31 23:41:08Z giovanni $
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=banshee
pkgver=1.6.0
pkgrel=1
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee-project.org/"
license=('MIT')
depends=('sqlite3' 'ndesk-dbus-glib' 'mono-addins' 'taglib-sharp' 
         'gstreamer0.10-good-plugins' 'gstreamer0.10-base-plugins' 
         'gnome-sharp' 'libmtp' 'ipod-sharp' 'boo' 'mono-zeroconf' 
         'libxxf86vm' 'gnome-icon-theme' 'desktop-file-utils' 'webkit-sharp')
makedepends=('pkgconfig' 'intltool')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
	    'gstreamer0.10-bad-plugins: Extra media codecs'
	    'gstreamer0.10-ffmpeg: Extra media codecs'
	    'brasero: CD burning'
	    'notify-sharp-svn: Display Banshee tray icon')
options=('!libtool' '!makeflags' '!emptydirs')
install=banshee.install
source=(http://download.banshee-project.org/${pkgname}/stable/${pkgver}/${pkgname}-1-${pkgver}.tar.bz2)
md5sums=('4ab6f48198627ed7a9a10b13c2656cd2')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
  cd "${srcdir}/${pkgname}-1-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-docs \
              --disable-static \
              --disable-youtube || return 1
  make || return 1
}

package() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
  cd "${srcdir}/${pkgname}-1-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain banshee-1 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
