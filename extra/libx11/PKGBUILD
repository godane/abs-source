# $Id: PKGBUILD 66498 2010-01-31 11:21:02Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libx11
pkgver=1.3.3
pkgrel=1
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb>=1.5' 'kbproto>=1.0.4')
makedepends=('xproto>=7.0.16' 'xextproto>=7.1.1' 'xf86bigfontproto>=1.2.0' 'xtrans>=1.2.5' 'inputproto>=2.0' 'xorg-util-macros>=1.4.1')
options=('!libtool')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
        xorg.sh)
sha1sums=('2a19724ccf78b5bb5a8eba9159d2b95e640e7b11'
          'b59214fe7d1d213f86b074a677eb624e52142ad7')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  ./configure --prefix=/usr \
      --with-xcb --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/xorg.sh" "${pkgdir}/etc/profile.d/" || return 1

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
