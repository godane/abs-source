# $Id: PKGBUILD 68222 2010-02-11 19:10:44Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=mkinitcpio-busybox
pkgver=1.15.3
pkgrel=5
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
source=(http://busybox.net/downloads/busybox-${pkgver}.tar.bz2
        config)
sha256sums=('d74020ad2cc5a4dcc5109c44dbd0e22582d6ce42954b0f1ff29763c8c0ff03cb'
            'abd66b978171447c20532ea23dd0a474c2c083d88658665fb1b7e2c3503a9bfe')

build() {
  cd "${srcdir}/busybox-${pkgver}"

  unset CFLAGS CXXFLAGS
  if [ ${CARCH} = "x86_64" ]; then
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=x86-64 -mtune=generic -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  else
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=i686 -mtune=generic -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  fi
  make || return 1
}

package() {
  install -D -m755 "${srcdir}/busybox-${pkgver}"/busybox "${pkgdir}/lib/initcpio/busybox"
}
# vim:set ts=2 sw=2 et:
