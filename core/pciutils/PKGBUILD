# $Id: PKGBUILD 69314 2010-02-20 10:20:37Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pciutils
pkgver=3.1.7
pkgrel=1
pkgdesc="PCI bus configuration space access library and tools"
arch=(i686 x86_64)
license=('GPL2')
groups=('base')
url="http://mj.ucw.cz/pciutils.html"
optdepends=('sh: required by update-pciids') 
makedepends=('wget')
depends=('glibc')
source=(ftp://ftp.kernel.org/pub/software/utils/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('35222edb86b50a6641352c66fe829b2e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  CFLAGS="${CFLAGS} -fPIC -DPIC"
  sed -i -e 's/^OPT=.*/OPT=-O2 -pipe -fPIC -DPIC/g' Makefile
  ./update-pciids.sh

  make ZLIB=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all || return 1
  make PREFIX=${pkgdir}/usr SHAREDIR=${pkgdir}/usr/share/hwdata MANDIR=${pkgdir}/usr/share/man install
  install -d ${pkgdir}/usr/lib
  install -m644 lib/libpci.a ${pkgdir}/usr/lib
  for i in config.h header.h pci.h types.h; do
    install -D -m 644 lib/${i} ${pkgdir}/usr/include/pci/${i}
  done
}
