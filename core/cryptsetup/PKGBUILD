# $Id: PKGBUILD 81542 2010-05-31 17:45:41Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=cryptsetup
pkgver=1.1.2
pkgrel=1
pkgdesc="Userspace setup tool for transparent encryption of block devices using the Linux 2.6 cryptoapi"
arch=(i686 x86_64)
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'e2fsprogs')
conflicts=('mkinitcpio<0.5.99')
options=('!libtool' '!emptydirs')
source=(http://cryptsetup.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2
        encrypt_hook
	encrypt_install)
sha256sums=('45972839fbb169b3ad5649454c217b7f5b0b3c8bf07e32f51a334a88217c7293'
            '64601eae6fbf3e3afceccec5877557aa208a82497c33cc94ad0a686b4022b5dc'
            '8e4920bb4b5ce96508aa0c42b9b07326b70daf630519f1aa1d8082bca709c12a')

build() {
  cd $srcdir/$pkgname-${pkgver}
  ./configure --prefix=/usr --disable-static --sbindir=/sbin --libdir=/lib
  make || return 1
  make DESTDIR=$pkgdir install
  # install hook
  install -D -m644 $srcdir/encrypt_hook $pkgdir/lib/initcpio/hooks/encrypt || return 1
  install -D -m644 $srcdir/encrypt_install $pkgdir/lib/initcpio/install/encrypt || return 1
}
