# $Id: PKGBUILD 72935 2010-03-21 16:14:26Z tpowa $
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=hdparm
pkgver=9.28
pkgrel=1
pkgdesc="A shell utility for manipulating Linux IDE drive/driver parameters"
arch=(i686 x86_64)
depends=('glibc')
optdepends=('sh: required by idectl and ultrabayd')
source=(http://downloads.sourceforge.net/sourceforge/hdparm/${pkgname}-${pkgver}.tar.gz)
license=('BSD')
url="http://sourceforge.net/projects/hdparm/"
options=('emptydirs')
md5sums=('b672d175bbf924b3f7575a8279eb063d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver} || return 1

  # build
  make || return 1

  # install
  mkdir -p ${pkgdir}/{usr,sbin} || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -m755 contrib/idectl ${pkgdir}/sbin || return 1
  install -m755 contrib/ultrabayd ${pkgdir}/sbin || return 1

  #install license file
  install -D -m 644 LICENSE.TXT $pkgdir/usr/share/licenses/hdparm/LICENSE.TXT || return 1
}
