# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Xavier Chantry <shiningxc@gmail.com>

pkgname=libfetch
pkgver=2.31
pkgrel=1
pkgdesc="URL based download library"
arch=('i686' 'x86_64')
license=('BSD')
depends=('openssl>=1.0.0')
url="http://cvsweb.netbsd.org/bsdweb.cgi/pkgsrc/net/libfetch/"
source=(Makefile
        ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('bb6c1fb99633ff3bea83cab0c3fdb394'
         '4c1bd75daeaf014cd79da097a2d794b1')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

# source PKGBUILD && mksource
mksource() {
  export CVSROOT=:pserver:anoncvs@anoncvs.NetBSD.org:/cvsroot
  D=pkgsrc/net/libfetch
  [ -d "$D" ] && cvs up "$D" || cvs co "$D"
  pushd "$D"
  dirname=$(sed -n 's/DISTNAME=.*\(libfetch-.*$\)/\1/p' Makefile)
  cp -r files $dirname
  tar -czv --exclude=CVS -f ../../../$dirname.tar.gz $dirname
  rm -r $dirname
  popd
}

build() {
  cd $srcdir/${pkgname}-${pkgver}
  cp ../Makefile .
  make || return 1
  make DESTDIR=$pkgdir install
}
